module nabl2/runtime/solver/solution

imports

  nabl2shared
  nabl2/runtime/common/-
  nabl2/runtime/prelude/-
  nabl2/runtime/solver/-

  signatures/nabl2/runtime/solver/-

rules
 
  sol-new = ![
    ScopeGraph(<G-new>),
    NameResolution([]),
    FullNameResolution([]),
    Typing([]),
    Values([]),
    Substitution([]),
    SubtypeRelation(<initial-subtype-relation>),
    Uninterpreted([],[]),
    Errors([]),
    Warnings([]),
    Notes([])
  ]

  sol-g       = fetch-elem(?ScopeGraph(<id>))
  sol-r       = fetch-elem(?NameResolution(<id>))
  sol-fr      = fetch-elem(?FullNameResolution(<id>))
  sol-t       = fetch-elem(?Typing(<id>))
  sol-v       = fetch-elem(?Values(<id>))
  sol-s       = fetch-elem(?Substitution(<id>))
  sol-x       = fetch-elem(?SubtypeRelation(<id>))
  sol-u-goals = fetch-elem(?Uninterpreted(<id>,_))
  sol-u-facts = fetch-elem(?Uninterpreted(_,<id>))
  sol-e       = fetch-elem(?Errors(<id>))
  sol-w       = fetch-elem(?Warnings(<id>))
  sol-n       = fetch-elem(?Notes(<id>))
 
  sol-with-g(|g) = fetch(ScopeGraph(!g))

  sol-apply-s(|s') =
    map(Substitution(<S-compose(is-var)> (s',<id>)) <+ <S-apply(is-var)> (s',<id>))

  sol-set-t(|o,ty) = fetch(Typing(set(!ty|o)))

  sol-add-v(|o,val) = fetch(Values(update(![val|<id>],![]|o)))

  sol-set-x(|x*) = fetch(SubtypeRelation(!x*))

  sol-set-r(|r,d) = fetch(NameResolution(set(!d|r)))

  sol-set-fr(|fr*) = fetch(FullNameResolution(!fr*))

  sol-set-fr(|r,d*) = fetch(FullNameResolution(set(!d*|r)))

  sol-add-u-goal(|g) = fetch(Uninterpreted(![g|<id>],id))
  sol-add-u-fact(|f) = fetch(Uninterpreted(id,![f|<id>]))

  sol-add-error(|msg) = fetch(Errors(set-add(message-eq|msg)))
  sol-add-warning(|msg) = fetch(Warnings(set-add(message-eq|msg)))
  sol-add-note(|msg) = fetch(Notes(set-add(message-eq|msg)))

  is-erronious = fetch(?Errors([_|_]))
