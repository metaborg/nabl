module nabl2/runtime/solver/solver

imports

  nabl2/runtime/analysis/-
  nabl2/runtime/prelude/-
  nabl2/runtime/solver/-

signature

  constructors
    Active : Term -> Liveness
    Done   : Term -> Liveness

rules 
 
  // nabl2--csolve-exhaustive :: (sol,[c]) -> [(sol,[c])] 
  // solve for all possible solutions

  nabl2--csolve-exhaustive(pre,solve,post,s-cost,c-lt) = nabl2--strict(
    map((id,nabl2--csolve-one-sort(c-lt)));
    mapconcat(nabl2--csolve-exhaustive-acc(pre,solve,post,s-cost,c-lt));
   nabl2--time-analysis(!"solution sorting",qsort(nabl2--csolve-exhaustive-lt(s-cost)))
  )

  nabl2--csolve-exhaustive-lt(s-cost) =
    nabl2--strict((s-cost,s-cost));
    lt

  nabl2--csolve-exhaustive-acc(pre,solve,post,s-cost,c-lt) = nabl2--strict(
    nabl2--csolve-one(pre,solve,c-lt)
  < mapconcat(nabl2--csolve-exhaustive-acc(pre,solve,post,s-cost,c-lt))
  + ![<post>]
  )
 
  // nabl2--csolve-greedy :: (sol,[c]) -> [(sol,[c])] 
  // solve for the best solution (solve all good
  // branches exhaustively)

  nabl2--csolve-greedy(pre,solve,post,s-cost,c-lt) = nabl2--strict(
    map((id,nabl2--csolve-one-sort(c-lt)));
    map(!Active(<id>));
    nabl2--csolve-greedy-sort(s-cost);
    nabl2--csolve-greedy-acc(pre,solve,post,s-cost,id,c-lt);
    map(nabl2--csolve-unwrap(id))
  )
 
  nabl2--csolve-greedy-acc(pre,solve,post,s-cost,s-guard,c-lt) = nabl2--strict(
    if Hd;nabl2--csolve-unwrap(s-cost);s-guard then
      if ?[Active(s)|s*] then
        if s'* := <nabl2--csolve-one(pre,solve,c-lt)> s then
          <map(!Active(<id>));nabl2--csolve-greedy-sort(s-cost)> s'*
        else
          ![Done(<post> s)]
        end;
        <nabl2--csolve-greedy-insert(s-cost)> (<id>,s*);
        nabl2--csolve-greedy-acc(pre,solve,post,s-cost,s-guard,c-lt)
      else
        if ?[s,s'|s*]; <nabl2--csolve-unwrap(s-cost);?0> s' then
          ![s'|s*];
          nabl2--csolve-greedy-acc(pre,solve,post,s-cost,?0,c-lt);
          ![s|<id>]
        end
      end
    end
  )

  nabl2--csolve-greedy-insert(s-cost) =
   nabl2--time-analysis(!"solution sorting",nabl2--merge-sorted(nabl2--csolve-greedy-lt(s-cost)))
    

  // compare solutions, first by errors, then active over done
  nabl2--csolve-greedy-lt(s-cost) = nabl2--strict(
    ?ss;
    cc := <(nabl2--csolve-unwrap(s-cost),nabl2--csolve-unwrap(s-cost))>
  );
  ( <lt> cc
 <+ <eq> cc;
    (Done(_),Active(_)) := ss
  )
 
  // sort list of solutions
  nabl2--csolve-greedy-sort(s-cost) =
   nabl2--time-analysis(!"solution sorting",qsort(nabl2--csolve-greedy-lt(s-cost)))

  nabl2--csolve-apply(s)  = nabl2--strict(Active(s) + Done(s))
  nabl2--csolve-unwrap(s) = nabl2--strict(?Active(<s>) + ?Done(<s>))

  // nabl2--csolve-one :: (sol,[c]) -> [(sol,[c])]
  // solve one constraint, or fail
 
  nabl2--csolve-one(pre,solve,c-lt) =
    ?(sol,active*);
    <nabl2--csolve-one-acc(pre,solve,c-lt)> (sol,active*,[])

  nabl2--csolve-one-acc(pre,solve,c-lt) =
    ?(sol,[c|active*],defer*);
    (sol',c') := <try(pre)> (sol,c);
    ( s'* := <solve> (c',sol')
    < <map({ sol', new*, new'*, active'*:
        ?(sol',new*);
        new'* :=  <qsort(c-lt)> new*;
        active'* := <nabl2--csolve-one-insert(c-lt)> (new'*,active*);
        !(sol', [active'*,defer*])
      })> s'*
    + <nabl2--csolve-one-acc(pre,solve,c-lt)> (sol,active*,[c'|defer*])
    )

    nabl2--csolve-one-sort(c-lt) =
     nabl2--time-analysis(!"constraint sorting",qsort(c-lt))

    nabl2--csolve-one-insert(c-lt) =
     nabl2--time-analysis(!"constraint sorting",nabl2--merge-sorted(c-lt))
 