module nabl2/runtime/solver/namebinding

imports
 
  nabl2/api
 
  signatures/nabl2/shared/common/-
  signatures/nabl2/shared/constraints/-
  pp/nabl2/shared/common/-
  nabl2/shared/-

  signatures/nabl2/runtime/solver/-
  nabl2/runtime/analysis/-
  nabl2/runtime/common/-
  nabl2/runtime/prelude/-
  nabl2/runtime/solver/-

rules
 
  nabl2--solve-fact = nabl2--time-analysis(!"scopegraph",nabl2--solve-scopegraph-fact)

  nabl2--solve-scopegraph-fact: (CGDecl(d,s),sol) -> [(sol',[])]
  where <nabl2--strict(is-NaBL2-Occurrence|"In <Occurrence> <- <Scope>: Occurrence must be ground")> d;
        <nabl2--strict(is-NaBL2-Scope|"In <Occurrence> <- <Scope>: scope must be ground")> s
  with g := <nabl2--sol-g> sol => g;
       g' := <nabl2--G-with-decl> (g,s,d);
       sol' := <nabl2--sol-with-g(|g')> sol

  nabl2--solve-scopegraph-fact: (CGRef(r,s),sol) -> [(sol',[])]
  where <nabl2--strict(is-NaBL2-Occurrence|"In <Occurrence> -> <Scope>: Occurrence must be ground")> r;
        <nabl2--strict(is-NaBL2-Scope|"In <Occurrence> -> <Scope>: scope must be ground")> s
  with g := <nabl2--sol-g> sol;
       g' := <nabl2--G-with-ref> (g,s,r);
       sol' := <nabl2--sol-with-g(|g')> sol

  nabl2--solve-scopegraph-fact: (CGDirectEdge(s,l,s'),sol) -> [(sol',[])]
  where <nabl2--strict(is-NaBL2-Scope|"In <Scope> ---> <Scope>: first scope must be ground")> s;
        <nabl2--strict(is-NaBL2-Scope + nabl2--is-var|"In <Scope> ---> <Scope>: second scope must be ground, or a variable")> s'
  with g := <nabl2--sol-g> sol;
       g' := <nabl2--G-with-direct-edge> (g,s,(l,s'));
       sol' := <nabl2--sol-with-g(|g')> sol

  nabl2--solve-scopegraph-fact: (CGNamedEdge(r,l,s),sol) -> [(sol',[])]
  where <nabl2--strict(is-NaBL2-Occurrence|"In <Occurrence> ===> <Scope>: Occurrence must be ground")> r;
        <nabl2--strict(is-NaBL2-Scope|"In <Occurrence> ===> <Scope>: scope must be ground")> s
  with g := <nabl2--sol-g> sol;
       g' := <nabl2--G-with-named-edge> (g,s,(l,r));
       sol' := <nabl2--sol-with-g(|g')> sol

  nabl2--solve-scopegraph-fact: (CGAssoc(d,l,s),sol)    -> [(sol',[])]
  where <nabl2--strict(is-NaBL2-Occurrence|"In <Occurrence> <=== <Scope>: Occurrence must be ground")> d;
        <nabl2--strict(is-NaBL2-Scope|"In <Occurrence> <=== <Scope>: scope must be ground")> s
  with g := <nabl2--sol-g> sol;
       g' := <nabl2--G-with-assoc> (g,d,(l,s));
       sol' := <nabl2--sol-with-g(|g')> sol

rules

  nabl2--solve-constraint =nabl2--time-analysis(!"namebinding",nabl2--solve-namebinding-constraint)

  nabl2--solve-namebinding-constraint: (CResolve(r,d,msginfo),sol) -> res*
  where <nabl2--is-ground> r;
        if (d',_) := <nabl2--sol-r;nabl2--get(|r)> sol then
           msg := ["Actual resolution ",d'," of ",r," differs from expected ",d];
           msginfo' := <nabl2--default-message(|msg)> msginfo;
           ![(sol, [CEqual(d,d',msginfo')])]
         else
           if dp* := <nabl2--sol-fr;nabl2--get(|r)> sol then
             !sol
           else
             dp* := <nabl2--resolve(|<nabl2--sol-g> sol)> r;
             <nabl2--sol-set-fr(|r,dp*)> sol
           end => sol';
           if [] := dp* then
             msg := [r," does not resolve to any declaration"];
             ![(<nabl2--add-message(nabl2--detail-message(|msg)|msginfo)> sol',[])]
           else if [_] := dp*
                 + <nabl2--occurrence-ns;nabl2-branch-on-ambiguous-references-hook> r then
             <map({msg, msginfo', sol'', c*:
                   \ (d',p) -> (sol'', c*)
                     where msg := ["Actual resolution ",d'," of ",r," differs from expected ",d];
                           msginfo' := <nabl2--default-message(|msg)> msginfo;
                           sol'' := <nabl2--sol-set-r(|r,(d',p))> sol';
                           c* := [CEqual(d,d',msginfo')]
                  \})> dp*
           else
             msg := ["Resolution of ",r," is ambiguous"];
             ![(<nabl2--add-message(nabl2--detail-message(|msg)|msginfo)> sol',[])]
           end end
         end => res*

  nabl2--solve-namebinding-constraint: (CAssoc(d,l,s,msginfo),sol) -> [res]
  where <nabl2--is-ground> d;
        g := <nabl2--sol-g> sol
  with ( s' := <nabl2--G-assocs;nabl2--get(|l)> (g,d)
       < msg := ["Problem associating scope ",s," with ",d];
         !(sol,[CEqual(s,s',<nabl2--default-message(|msg)> msginfo)])
       + msg := [d," is not associated with a ",l," scope"];
         sol' := <nabl2--add-message(nabl2--detail-message(|msg)|msginfo)> sol;
         !(sol',[])
       ) => res
 
rules

  nabl2-is-op-hook = is-NaBL2-Names

  nabl2-reduce-op-hook(|sol) = nabl2--get-names(|<nabl2--sol-g> sol)

  nabl2--apply-elems-message(|x*): msginfo@Message(kind,message,pos) -> msg*
    where <collect-one(?NAME())> msginfo
    with msg* := <map(\ x -> Message(kind, <bottomup(try(\ NAME() -> <nabl2--occurrence-name> x \))> message, <try(\ NAME() -> x \)> pos ) \)> x*

rules

  nabl2--postprocess-constraint = nabl2--postprocess-namebinding-constraint

  nabl2--postprocess-namebinding-constraint: (CResolve(r,_,msginfo), sol) -> sol'
  with msg := ["Failed to resolve ",r];
       sol' := <nabl2--add-message(nabl2--default-message(|msg)|msginfo)> sol

  nabl2--postprocess-namebinding-constraint: (CAssoc(d,l,_,msginfo), sol) -> sol
  with msg := ["Failed to get associated scope with label ",l," of ",d];
       sol' := <nabl2--add-message(nabl2--default-message(|msg)|msginfo)> sol


rules

  nabl2--constraint-priority: CAssoc(_,_,_,_) -> 2
  nabl2--constraint-priority: CResolve(_,_,_) -> 2

rules
 
  nabl2--postprocess-namebinding =
    nabl2--add-unresolved-ref-errors;
    nabl2--ensure-all-scopes

  nabl2--add-unresolved-ref-errors: sol -> sol'
    with nabl2--time-analysis(!"namebinding",
         g := <nabl2--sol-g> sol;
         ref* := <conc> (<nabl2--G-refs> g, <nabl2--G-imports> g);
         res* := <nabl2--sol-fr;nabl2--keys> sol;
         unknown* := <diff;filter(where(nabl2--occurrence-ns;
                                        nabl2-resolve-unused-references-hook))> (ref*, res*);
         sol' := <foldr(!sol,{ ref,sol, dp*:
           ?(ref,sol);
           dp* := <nabl2--resolve(|g)<+![]> ref;
           <nabl2--sol-set-fr(|ref,dp*)> sol;
           if [] := dp* then
             nabl2--add-message(id|Message(Error(),["Failed to resolve ",ref],<nabl2--occurrence-index> ref))
           else if [dp] := dp* then
             nabl2--sol-set-r(|ref,dp)
           else 
             nabl2--add-message(id|Message(Error(),["Ambiguous resolution for ",ref],<nabl2--occurrence-index> ref))
           end end
         })> unknown*
    )

  nabl2--ensure-all-scopes: sol -> sol'
    with nabl2--time-analysis(!"namebinding",
      ss := <collect-all(?Scope(_,_))> sol;
      g := <nabl2--sol-g> sol;
      g' := <foldr(!g,flip(nabl2--G-ensure-scope))> ss;
      sol' := <nabl2--sol-with-g(|g')> sol
    )
