module nabl2/runtime/interpreter/util

imports

  nabl2/runtime/interpreter/scopegraph-api

signature

  arrows // Auxiliary functions
    filterOccsByNamespace(List(Occurrence), Namespace) --> List(Occurrence)
    declOfPath(List(Step)) --> Occurrence
    NaBL2 |- declsOf(Scope, Namespace) --> List(Occurrence)
    NaBL2 |- declOfRef(Occurrence) --> Occurrence
    NaBL2 |- assocScopeOf(Occurrence, Label) --> Scope

rules

  filterOccsByNamespace([], _) --> []
  
  filterOccsByNamespace([o|os], ns) --> [o|filterOccsByNamespace(os, ns)]
  where
    ns == namespaceOf(o)
    
  filterOccsByNamespace([o|os], ns) --> filterOccsByNamespace(os, ns)
  where
    ns != namespaceOf(o)
    
rules    
 
  declOfRef(o:Occurrence) --> declOfPath(p)
  where
    pathOf(o) --> p

rules

  declOfPath([D(_, o)]) --> o

  declOfPath([_|p]) --> declOfPath(p)

rules

  declsOf(s, ns) /* :(Scope * Namespace) */ --> decls'
    where declsOf(s) --> decls;
          filterOccsByNamespace(decls, ns) => decls'

  assocScopeOf(d, l) /* :(Occurrence * Label) */ --> s
  where
    assocsOf(d) --> assocs;
    assocs[l] => [s]
