module nabl2/runtime/common/subtype-relation

imports

  nabl2/shared/-

  nabl2/runtime/common/-
  nabl2/runtime/prelude/-
  nabl2/runtime/pp

signature

  constructors
    covar     : Variance
    contravar : Variance
    Wld       : Pattern

rules

  nabl2--type-sig-of-hook = fail


  nabl2--initial-subtype-relation = ![]
 

  nabl2--extend-subtype-relation(|x*): (ty1,ty2) -> [x|x'*]
    where ety1 := <nabl2--erase-subtypeterms> ty1;
          ety2 := <nabl2--erase-subtypeterms> ty2;
          <not(getfirst((where(nabl2--erase-subtypeterms;?ety1),id)))> x*;
          not(<nabl2--upcast(|x*)> (ty2,ty1))
    with if (ty2',supty2*) := <getfirst((where(nabl2--erase-subtypeterms;?ety2),id))> x* then
           tvar2* := <nabl2--get-subtypeterms> ty2;
           tvar2'* := <nabl2--get-subtypeterms> ty2';
           s := <zip> (tvar2'*,tvar2*);
           supty2'* := <nabl2--S-apply(is-NaBL2-TypeVar)> (s,supty2*)
         else
           supty2'* := []
         end;
         supty1* := [ty2|supty2'*];
         x := (ty1,supty1*);
         tvar1* := <nabl2--get-subtypeterms> ty1;
         x'* := <map(try({ ty1', ttrm1'*, s, supty1'*: \
           (ty,supty*) -> (ty,[supty*,supty1'*])
           where ty1' := <getfirst(where(nabl2--erase-subtypeterms;?ety1))> supty*;
                 ttrm1'* := <nabl2--get-subtypeterms> ty1';
                 s := <zip> (tvar1*,ttrm1'*);
                 supty1'* := <nabl2--S-apply(is-NaBL2-TypeVar)> (s,supty1*)
         \}))> x*
 
  nabl2--extend-subtype-relation(|x*): (ty1,ty2) -> x*
    where ety1 := <nabl2--erase-subtypeterms> ty1;
          (ty1',ty2') := <getfirst((where(nabl2--erase-subtypeterms;?ety1),Hd))> x*;
          tvar* := <nabl2--get-subtypeterms> ty1;
          tvar'* := <nabl2--get-subtypeterms> ty1';
          s := <zip> (tvar*,tvar'*);
          ty2'' := <nabl2--S-apply(is-NaBL2-TypeVar)> (s,ty2);
          <nabl2--term-eq> (ty2',ty2'')


  nabl2--is-subtype(|x*):
    (ty1,ty2) -> <nabl2--is-subtype(|x*)> (ty1,covar(),ty2)

  nabl2--is-subtype(|x*):
    (ty1,_,ty2) -> <id>
    where <nabl2--term-eq> (ty1,ty2)

  nabl2--is-subtype(|x*):
    (ty1,contravar(),ty2) -> <id>
    where <nabl2--is-subtype(|x*)> (ty2,covar(),ty1)

  nabl2--is-subtype(|x*):
    (ty1*,covar(),ty2*) -> <id>
    where <is-list> ty1*;
          <is-list> ty2*;
          <zip;map(\ (ty1,ty2) -> <nabl2--is-subtype(|x*)> (ty1,covar(),ty2) \)> (ty1*,ty2*)

  nabl2--is-subtype(|x*):
    (ty1,covar(),ty2) -> <id>
    where <not(is-list)> ty1;
          <not(is-list)> ty2;
          ty2' := <nabl2--upcast(|x*)> (ty1,ty2);
          var*    := <nabl2--get-subtypevariances> ty2';
          subty*  := <nabl2--get-subtypeterms> ty2;
          subty'* := <nabl2--get-subtypeterms> ty2';
          <nabl2--zipN;map(nabl2--is-subtype(|x*))> (subty'*,var*,subty*)
 

  nabl2--upcast(|x*):
    (ty1,ty2) -> ty1
    where ety1 := <nabl2--erase-subtypeterms> ty1;
          ety2 := <nabl2--erase-subtypeterms> ty2;
          <nabl2--term-eq> (ety1,ety2)

  nabl2--upcast(|x*):
    (ty1,ty2) -> ty2''
    where ety1 := <nabl2--erase-subtypeterms> ty1;
          ety2 := <nabl2--erase-subtypeterms> ty2;
          <not(nabl2--term-eq)> (ety1,ety2);
          (ty1',ty2') := <getfirst((where(nabl2--erase-subtypeterms;?ety1),getfirst(where(nabl2--erase-subtypeterms;?ety2))))> x*;
          ttrm* := <nabl2--get-subtypeterms> ty1;
          tvar* := <nabl2--get-subtypeterms> ty1';
          s := <zip> (tvar*,ttrm*);
          ty2'' := <nabl2--S-apply(is-NaBL2-TypeVar)> (s,ty2')
 

  nabl2--erase-subtypeterms: ty@c#(a*) -> c#(a'*)
    with tsort := <type-sort>
    where sig* := <nabl2--type-sig-of-hook<+fatal-err(|"Missing type signature for ")> ty;
          a'* := <zip;map(?((_,tsort),_) < !Wld() + Snd)> (sig*,a*)

  nabl2--get-subtypeterms: ty@c#(a*) -> ty*
    with tsort := <type-sort>
    where sig* := <nabl2--type-sig-of-hook<+fatal-err(|"Missing type signature for ")> ty;
          ty* := <zip;filter(\ ((_,tsort),ty) -> ty \)> (sig*,a*)
 
  nabl2--get-subtypevariances: ty -> variance*
    with tsort := <type-sort>
    where sig* := <nabl2--type-sig-of-hook<+fatal-err(|"Missing type signature for ")> ty;
          variance* := <filter(\ (var,tsort) -> var \)> sig*
 
  type-sort = !SortNoArgs("Type")
