module statix/lang/Sugar

imports

  statix/lang/Common
  statix/lang/Core

template options

  keyword -/- [a-zA-Z0-9\_\-\']


/******************************************************************
 * User Constraints                                               *
 ******************************************************************/

context-free syntax

  // lifting of constraints

  Rule             = <constraint <CDerive>>
  Rule             = CDerive

  Rule.CDecl       = [[CInd]constraint [ConstraintId] : [Type]]
  Rule.CDecl       = [[CInd][ConstraintId] : [Type]]

  CDecl            = CDerive

  CDerive.CDeriveMap = [[ConstraintId] maps [ConstraintId]([{Lift ", "}*])]
  CDerive.FDeriveMap = [[ConstraintId] maps [ConstraintId]([{Lift ", "}*]) = [Lift]]

  Lift.IdLift     = "*"
  Lift.ListLift   = "list(*)"
  Lift.Tuple0Lift = <( )>
  Lift.TupleNLift = <(<Lift>, <{Lift ", "}+>)>

  // rule shorthands

  Rule.AxiomRule = <
    <RuleName><RuleHead>.
  >

  RuleHead.F = [[ConstraintId]([{Term ", "}*]) = [Term]]

  HoConstraint.LLam = <{ <{Term ", "}+> }>

  HoConstraint.LC = ConstraintId

syntax

  Term-CF.COp = ConstraintId-CF "(" LAYOUT?-CF CommaTerms-CF LAYOUT?-CF ")"

lexical syntax

  Keyword = "constraint" | "maps"


/******************************************************************
 * Base Constraints                                               *
 ******************************************************************/

context-free syntax

  HoConstraint.LTrue  = "true"
  HoConstraint.LFalse = "false"

  Term.NewOp   = "new"
  Term.AstIdOp = <astId(<Term>)>

  AstProperty.Ref  = "ref"
  AstProperty.Type = "type"

lexical syntax

  VARID = "new" {reject}

  AstPropertyId = "ref" | "type" {reject}


/******************************************************************
 * Relations                                                      *
 ******************************************************************/

context-free syntax

  RelRef.Decl = "decl"

syntax

  // short-hand for querying relations

  Constraint-CF.CAskRel  = "?" RelRef-CF "[" CommaTerms-CF "]" LAYOUT?-CF "in" LAYOUT?-CF Term-CF LAYOUT?-CF Message-CF

  Term-CF.AskRelOp = "?" RelRef-CF "[" LAYOUT?-CF CommaTerms-CF LAYOUT?-CF "]" LAYOUT?-CF "in" LAYOUT?-CF Term-CF

lexical syntax

  RelationId = "decl" {reject}
  RelationId = "ref" {reject}


/******************************************************************
 * Scope Graph                                                    *
 ******************************************************************/

context-free syntax

  LabelRE.ZeroOrOne = <<LabelRE>?>
  LabelRE.OneOrMore = <<LabelRE>+>

context-free priorities

    { LabelRE.Neg LabelRE.Closure LabelRE.ZeroOrOne LabelRE.OneOrMore }
  > { LabelRE.Concat LabelRE.And LabelRE.Or }

lexical syntax

  Keyword = "resolve" | "filter" | "with" | "and"


/******************************************************************
 * Resolve & Query
 ******************************************************************/

context-free syntax

  // short-hand forms for occurrences using default resolution policies

  QueryFilter.NoFilter =
  QueryFilter.Filter   = < filter <LabelRE>>


  QueryMin.NoMin =
  QueryMin.Min   = < min <LabelOrd>>

lexical syntax

  Keyword = "filter" | "min" | "in" | "of" | "resolveMatch" | "resolveLt" | "occurrenceMatch"


/******************************************************************
 * Sorts & Constructors                                           *
 ******************************************************************/

context-free syntax

  Type.FunType    = [[{SortRef " * "}*] -> [SortRef]]


/******************************************************************
 * Terms                                                          *
 ******************************************************************/


/******************************************************************
 * Arithmetic                                                          *
 ******************************************************************/

syntax

  Term-CF.ArithOp = "#" "(" LAYOUT?-CF ArithExp-CF LAYOUT?-CF ")"
