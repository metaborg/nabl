module statix/lang/statics/sugar

imports

  signatures/statix/lang/-

  statix/lang/statics/-

rules

/******************************************************************
 * Modules                                                        *
 ******************************************************************/


/******************************************************************
 * User Constraints                                               *
 ******************************************************************/

  DeclOrRule[[ CDeriveMap(x, x', lifts) ^ (s, s_syn) ]] :=
    C{x'} -> s, C{x'} |-> d | error $[Unresolved constraint [x']] @x', @x'.ref := C{x'}, @x'.decl := d,
    d : ty, ty != FUN(_, _) | error "Use `c maps c(...) = ...` for functional constraints." @x,
    d.ind := ind,

    tys is `in-types` of ty,
    lifts <`lifts-some`? () | error "At least one argument must be lifted.",
    tys' is `lift-types` of (lifts, tys),

    C{x} <- s, @x.decl := C{x},
    C{x} : PRED(tys') !!,
    C{x}.ind := ind !!,
    C{x}.kind := DerivedC() !!,
    C{x}.qname := qname, This{()} -> s, This{()} |-> d_this, d_this.qname := qname.

  DeclOrRule[[ FDeriveMap(x, x', inlifts, outlift) ^ (s, s_syn) ]] :=
    C{x'} -> s, C{x'} |-> d | error $[Unresolved constraint [x']] @x', @x'.ref := C{x'}, @x'.decl := d,
    d : ty, ty != PRED(_) | error "Use `c maps c(...)` for non-functional constraints." @x,
    d.ind := ind,

    intys is `in-types` of ty,
    outty is `out-type` of ty,
    inlifts <`lifts-some`? () | error "At least one input argument must be lifted.",
    intys' is `lift-types` of (inlifts, intys),
    outty' is `lift-type` of (outlift, outty),

    C{x} <- s, @x.decl := C{x},
    C{x} : FUN(intys', outty') !!,
    C{x}.ind := ind !!,
    C{x}.kind := DerivedC() !!,
    C{x}.qname := qname, This{()} -> s, This{()} |-> d_this, d_this.qname := qname.

  RuleHead[[ h@F(x, inargs, outarg) ^ (s_rule, s_syn) : ty ]] :=
    C{x} -> s_rule, C{x} |-> d | error $[Unresolved constraint [x]] @x, @x.ref := C{x}, @x.decl := d,
    d.kind := DeclaredC() | error "Rules can only be defined for declared constraints.",
    Rule{x} <- s_syn, Rule{x}.decl := d, Rule{x}.head := h,
    d : ty, ty != PRED(_) | error "Use `c(...) | ...` for non-functional constraints." @x,
    intys is `in-types` of ty,
    outty is `out-type` of ty,
    Map1T(Match)[[ inargs ^ (s_rule) : intys ]],
    Match[[ outarg ^ (s_rule) : outty ]].

  Term(V)[[ COp(x, args) ^ (s) : outty ]] :=
    C{x} -> s, C{x} |-> d | error $[Unresolved constraint [x]] @x, @x.ref := C{x}, @x.decl := d,
    d : ty', ty' != PRED(_) | error "Inline notation can only be used for functional constraints.",
    intys is `in-types` of ty',
    outty is `out-type` of ty',
    Map1T(Term(V))[[ args ^ (s) : intys ]].

  HoConstraint[[ LC(x) ^ (s) : ty ]] :=
    C{x} -> s, C{x} |-> d | error $[Unresolved constraint [x]] @x, @x.ref := C{x}, @x.decl := d,
    d : ty.


/******************************************************************
 * Base Constraints                                               *
 ******************************************************************/

  HoConstraint[[ LTrue() ^ (_) : PRED(_) ]].

  HoConstraint[[ LFalse() ^ (_) : PRED(_) ]].

  Term(V)[[ NewOp() ^ (s) : SCOPE() ]].


/******************************************************************
 * AST Constraints                                                *
 ******************************************************************/

  Term(V)[[ AstIdOp(t) ^ (s) : AST_ID() ]] :=
    Term(V)[[ t ^ (s) : _ ]].


/******************************************************************
 * Relations                                                      *
 ******************************************************************/

  RelRef[[ l@Decl() ^ (s) : ty ]] :=
    Rel{l} -> s, Rel{l} |-> d, @l.ref := Rel{l}, @l.decl := d,
    d : ty.

  Constraint[[ CAskRel(rel, ts, st, msg) ^ (s) ]] :=
    RelRef[[ rel ^ (s) : ty ]], ty != FUN(_, _) | error "Use inline notation for functional relations.",
    tys is `in-types` of ty,
    Map1T(Build)[[ ts ^ (s): tys ]],
    Build[[ st ^ (s) : SCOPE() ]].

  Term(V)[[ AskRelOp(rel, ts, st) ^ (s) : outty ]] :=
    RelRef[[ rel ^ (s) : ty' ]], ty' != PRED(_) | error "Use predicate notation for non-functional relations.",
    intys is `in-types` of ty',
    outty is `out-type` of ty',
    Map1T(Term(V))[[ ts ^ (s) : intys ]],
    Term(V)[[ st ^ (s) : SCOPE() ]].


/******************************************************************
 * Scope Graph                                                    *
 ******************************************************************/
 
  DeclOrRule[[ NamespaceQuery(x, filter, min) ^ (s, s_syn) ]] :=
    Ns{x} -> s, Ns{x} |-> d | error $[Unresolved namespace [x]] @x, @x.ref := Ns{x}, @x.decl := d,
    NsQ{x} <- s,
    Filter[[ filter ^ (s, NsQ{x}) ]],
    Min[[ min ^ (s, NsQ{x}) ]],
    NsQ{x}.qname := qname, This{()} -> s, This{()} |-> d_this, d_this.qname := qname.

  Filter[[ NamespaceFilter(re) ^ (s, d) ]] :=
    d.wf := re,
    LabelRE[[ re ^ (s) ]].

  Min[[ NamespaceMin(prs) ^ (s,d ) ]] :=
    d.ord := prs,
    Map1(LabelPair)[[ prs ^ (s) ]].


/******************************************************************
 * Resolve & Query
 ******************************************************************/

  LabelRE[[ t@ResolveMatch(r@Occurrence(ns, _, _)) ^ (s) ]] :=
    Build[[ r ^ (s) : OCCURRENCE() ]],
    NsQ{ns} -> s, NsQ{ns} |-> d | error $[Unresolved resolution policy for namespace [ns]] @ns, @t.ref := NsQ{ns}, @t.decl := d.

  LabelOrd[[ t@ResolveLt(r@Occurrence(ns, _, _)) ^ (s) ]] :=
    Build[[ r ^ (s) : OCCURRENCE() ]],
    NsQ{ns} -> s, NsQ{ns} |-> d | error $[Unresolved resolution policy for namespace [ns]] @ns, @t.ref := NsQ{ns}, @t.decl := d.

  HoConstraint[[ OccurrenceMatch(r) ^ (s) : PRED([OCCURRENCE()]) ]] :=
    Build[[ r ^ (s) : OCCURRENCE() ]].


/******************************************************************
 * Sorts & Constructors                                           *
 ******************************************************************/

  Type[[ FunType(insorts, outsort) ^ (s) : FUN(intys, outty) ]] :=
    Map1T(SortRef)[[ insorts ^ (s) : intys ]],
    SortRef[[ outsort ^ (s) : outty ]].


/******************************************************************
 * Terms                                                          *
 ******************************************************************/


/******************************************************************
 * Arithmetic                                                     *
 ******************************************************************/

rules

  Term(V)[[ ArithOp(ae) ^ (s) : INT() ]] :=
    ArithExp(V)[[ ae ^ (s) : INT() ]].
