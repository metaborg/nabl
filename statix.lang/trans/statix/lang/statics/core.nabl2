module statix/lang/statics/core

imports

  signatures/statix/lang/-

  statix/lang/statics/-

signature

  sorts
    SKind
    CKind

  constructors
    SimpleSort : SKind
    SortAlias  : SKind

    DeclaredC : CKind
    DerivedC  : CKind

    UserOp : OpKind
    InjOp  : OpKind

  namespaces
    C       // constraint
    Rule    // rule
    Rel     // data label
    Edge    // edge label
    Mod     // module
    This    // current module
    Ns      // namespace
    NsQ     // namespace query
    Op      // term constructor
    Sort    // sort
    Barrier // barrier

  name resolution
    strategy environments
    paths irrelevant
    labels
      P  // lexical parent
      I  // module interface (qualified, non-transitively imported)
      S  // module syntax (unqualified, transitively imported)
      LS // local module syntax
    well-formedness
      P* (I? | LS? S*)
    order
      D < P,
      // module interface
      D < I,
      I < P,
      // module syntax
      D < S,
      LS < S

  relations
     reflexive, transitive, anti-symmetric inj : TYPE * TYPE {
//       (-sub, +sub),
//       [+sub]
     }
     
rules

  init ^ (s) :=
    new s,

    // check duplicates
    distinct/name D(s)/Mod | error $[Duplicate module [NAME]] @NAMES,
  
    // sugar

    Rel{Decl()} <- s,
    Rel{Decl()} : PRED([OCCURRENCE()]),
    Rel{Decl()}.qname := "".


/******************************************************************
 * Modules                                                        *
 ******************************************************************/

  [[ m@Module(x, sections) ^ (s) ]] :=
    // define module
    new s_mod, s_mod -P-> s,
    Mod{x} <- s, Mod{x} =I=> s_mod,
    // syntax part of the interface
    new s_syn, s_mod -LS-> s_syn,
    Mod{x} =S=> s_syn,
    // define the current module
    Mod{x}.qname := x,
    This{()} <- s_mod, This{()}.qname := x !!,
    // check sections
    Sections[[ sections ^ (s_mod, s_syn, s) ]].

  Sections[[ sections ^ (s, s_syn, s_outer) ]] :=
    Map3(Section)[[ sections ^ (s, s_syn, s_outer) ]],

    // duplicate checks for all non-transitively scoped identifiers
    distinct/name D(s)/C                     | error $[Duplicate constraint [NAME]] @NAMES,
    distinct/name D(s)/Ns                    | error $[Duplicate namespace [NAME]] @NAMES,
    distinct/name D(s)/NsQ                   | error $[Duplicate resolution policy for namespace [NAME]] @NAMES,
    distinct/name (D(s)/Rel union D(s)/Edge) | error $[Duplicate label/relation [NAME]] @NAMES,
    (D(s)/C                     lsect/name (W(s)/C                     minus D(s)/C                    )) subseteq 0 | warning $[Shadows imported constraint [NAME]] @NAMES,
    (D(s)/Ns                    lsect/name (W(s)/Ns                    minus D(s)/Ns                   )) subseteq 0 | warning $[Shadows imported namespace [NAME]] @NAMES,
    (D(s)/NsQ                   lsect/name (W(s)/NsQ                   minus D(s)/NsQ                  )) subseteq 0 | warning $[Shadows imported resolution policy for namespace [NAME]] @NAMES,
    ((D(s)/Rel union D(s)/Edge) lsect/name ((W(s)/Rel union W(s)/Edge) minus (D(s)/Rel union D(s)/Edge))) subseteq 0 | warning $[Shadows imported edge label / relation [NAME]] @NAMES,

    // duplicate checks for transitively scoped sorts and constructors
    distinct/name D(s_syn)/Op   | error $[Duplicate constructor [NAME]] @NAMES,
    distinct/name D(s_syn)/Sort | error $[Duplicate sort [NAME]] @NAMES,
    ?- DistinctPatterns(localRules),
        localRules is set D(s_syn)/Rule,
    (D(s_syn)/Sort lsect/name (W(s)/Sort minus D(s_syn)/Sort)) subseteq 0 | error $[Duplicate of imported sort [NAME]] @NAMES,
    (D(s_syn)/Op   lsect/name (W(s)/Op   minus D(s_syn)/Op  )) subseteq 0 | error $[Duplicate of imported constructor [NAME]] @NAMES,
    ?- NoDuplicatePatterns(localRules, importedRules, ()),
        importedRules is set (W(s)/Rule minus D(s_syn)/Rule).

  Section[[ Imports(imports) ^ (s, s_syn, s_outer) ]] :=
    Map3(Import)[[ imports ^ (s_outer, s, s_syn) ]].

  Import[[ Import(x) ^ (s, s_mod, s_syn) ]] :=
    Mod{x} -> s, Mod{x} |-> d | error $[Unresolved import module [x]] @x, @x.ref := Mod{x}, @x.decl := d,
    Mod{x} <=I= s_mod, d ?=I=> s_mod',
    (V(s_mod')/C                          lsect/name (V(s_mod)/C                         minus (D(s_mod)/C                         union V(s_mod')/C                         ))) subseteq 0 | error $[Duplicate imported constraints [NAME]] @x,
    (V(s_mod')/Ns                         lsect/name (V(s_mod)/Ns                        minus (D(s_mod)/Ns                        union V(s_mod')/Ns                        ))) subseteq 0 | error $[Duplicate imported namespaces [NAME]] @x,
    (V(s_mod')/NsQ                        lsect/name (V(s_mod)/NsQ                       minus (D(s_mod)/NsQ                       union V(s_mod')/NsQ                       ))) subseteq 0 | error $[Duplicate imported resolution policies for [NAME]] @x,
    ((V(s_mod')/Rel union V(s_mod')/Edge) lsect/name ((V(s_mod)/Rel union V(s_mod)/Edge) minus ((D(s_mod)/Rel union D(s_mod)/Edge) union (V(s_mod')/Rel union V(s_mod')/Edge)))) subseteq 0 | error $[Duplicate imported edge labels / relations [NAME]] @x,
    Mod{x} <=S= s_syn, d ?=S=> s_syn',
    (V(s_syn')/Sort lsect/name (V(s_syn)/Sort minus (D(s_syn)/Sort union V(s_syn')/Sort))) subseteq 0 | error $[Duplicate imported constructor [NAME]] @x,
    (V(s_syn')/Op   lsect/name (V(s_syn)/Op   minus (D(s_syn)/Op   union V(s_syn')/Op  ))) subseteq 0 | error $[Duplicate imported constructor [NAME]] @x,
    ?- NoDuplicatePatterns(importedRules, otherImportedRules, x),
        importedRules is set W(s_syn')/Rule,
        otherImportedRules is set (W(s_syn)/Rule minus (D(s_syn)/Rule union W(s_syn')/Rule)).

  Section[[ Signature(sigs) ^ (s, s_syn, _) ]] :=
    Map2(Signature)[[ sigs ^ (s, s_syn) ]].

  Section[[ Rules(rs) ^ (s, s_syn, _) ]] :=
    Map2(DeclOrRule)[[ rs ^ (s, s_syn) ]].


/******************************************************************
 * User Constraints                                               *
 ******************************************************************/

  Signature[[ Constraints(cs) ^ (s, s_syn) ]] :=
    Map2(DeclOrRule)[[ cs ^ (s, s_syn) ]].

  DeclOrRule[[ CDecl(ind, x, type) ^ (s, s_syn) ]] :=
    Ind[[ ind ]],
    Type[[ type ^ (s) : ty ]],

    C{x} <- s, @x.decl := C{x},
    C{x} : ty !!,
    C{x}.ind := ind !!,
    C{x}.kind := DeclaredC() !!,
    This{()} -> s, This{()} |-> d_this, d_this.qname := qname, C{x}.qname := qname.

  Ind[[ InductiveC() ]].
  Ind[[ CoinductiveC() ]] := false | error "Co-inductive constraints are not yet supported.".

  DeclOrRule[[ r@Rule(_, h, body) ^ (s, s_syn) ]] :=
    new s_rule, s_rule -P-> s,
    Barrier{()} <- s_rule, Barrier{()} =B=> s_rule,
    distinct/name D(s_rule)/Var | error $[Duplicate variable [NAME]] @NAMES,
    RuleHead[[ h ^ (s_rule, s_syn) : _ ]],
    Constraint[[ body ^ (s_rule) ]].

  RuleHead[[ h@C(x, args) ^ (s_rule, s_syn) : ty ]] :=
    C{x} -> s_rule, C{x} |-> d | error $[Unresolved constraint [x]] @x, @x.ref := C{x}, @x.decl := d,
    d.kind := DeclaredC() | error "Rules can only be defined for declared constraints.",
    Rule{x} <- s_syn, Rule{x}.decl := d, Rule{x}.head := h,
    d : ty, ty != FUN(_, _) | error "Use `c(...) = ... | ...` for functional constraints." @x,
    tys is `in-types` of ty,
    Map1T(Match)[[ args ^ (s_rule) : tys ]].

  Constraint[[ C(x, args, msg) ^ (s) ]] :=
    C{x} -> s, C{x} |-> d | error $[Unresolved constraint [x]] @x, @x.ref := C{x}, @x.decl := d,
    d : ty, ty != FUN(_, _) | error "Use inline notation for functional constraints.",
    tys is `in-types` of ty,
    Map1T(Build)[[ args ^ (s) : tys ]],
    Message(ErrorKind)[[ msg ^ (s) ]].

  HoConstraint[[ l@LLam(ps, c) ^ (s) : PRED(tys) ]] :=
    new s', s' -P-> s, @l.scope := s',
    Barrier{()} <- s', Barrier{()} =B=> s',
    Map1T(Match)[[ ps ^ (s') : tys ]],
    (D(s')/Var lsect/name V(s)/Var) subseteq 0 | warning $[Shadowing variable [NAME]] @NAMES,
    Constraint[[ c ^ (s') ]].


/******************************************************************
 * Base Constraints                                               *
 ******************************************************************/

  Constraint[[ CTrue() ^ (_) ]].

  Constraint[[ CFalse(msg) ^ (s) ]] :=
    Message(ErrorKind)[[ msg ^ (s) ]].

  Constraint[[ CConj(c1, c2) ^ (s) ]] :=
    Constraint[[ c1 ^ (s) ]],
    Constraint[[ c2 ^ (s) ]].

  Constraint[[ CNew(vs) ^ (s) ]] :=
    Map1t(VarRef)[[ vs ^ (s) : SCOPE() ]].

  Constraint[[ CEqual(t1, t2, msg) ^ (s) ]] :=
    Build[[ t1 ^ (s) : ty ]],
    Build[[ t2 ^ (s) : ty ]],
    Message(ErrorKind)[[ msg ^ (s) ]].

  Constraint[[ CInequal(t1, t2, msg) ^ (s) ]] :=
    Build[[ t1 ^ (s) : ty ]],
    Build[[ t2 ^ (s) : ty ]],
    Message(ErrorKind)[[ msg ^ (s) ]].

  Constraint[[ e@CExists(vs, c) ^ (s) ]] :=
    new s', s' -P-> s,
    Map1T(VarDecl)[[ vs ^ (s') : _ ]],
    distinct/name D(s')/Var | error $[Duplicate variable [NAME]] @NAMES,
    (D(s')/Var lsect/name V(s)/Var) subseteq 0 | warning $[Shadowing variable [NAME]] @NAMES,
    Constraint[[ c ^ (s') ]].

  Constraint[[ t@CTry(c, msg) ^ (s) ]] :=
    new s', s' -P-> s, @t.scope := s',
    Barrier{()} <- s', Barrier{()} =B=> s',
    Constraint[[ c ^ (s') ]],
    Message(TryKind)[[ msg ^ (s) ]].

/******************************************************************
 * AST Constraints                                                *
 ******************************************************************/


  Constraint[[ CAstId(t1, t2) ^ (s) ]] :=
    Build[[ t1 ^ (s) : _ ]],
    Build[[ t2 ^ (s) : AST_ID() ]].

  Constraint[[ CAstProperty(t1, l, op, t2) ^ (s) ]] :=
    TermOrId(Build)[[ t1 ^ (s) : _ ]],
    Build[[ t2 ^ (s) : _ ]].

  // only call this with a wildcard for ty!
  TermOrId(V)[[ t ^ (s) : ty ]] :=
    V[[ t ^ (s) : ty ]].

/******************************************************************
 * Relations                                                      *
 ******************************************************************/

  Signature[[ Relations(rdecls) ^ (s, s_syn) ]] :=
    Map2(DeclOrRule)[[ rdecls ^ (s, s_syn) ]].

  DeclOrRule[[ RelDecl(rel, type) ^ (s, s_syn) ]] :=
    Type[[ type ^ (s) : ty ]],
    RelDecl[[ rel ^ (s) : ty ]].

  RelDecl[[ r@Label(x) ^ (s) : ty ]] :=
    Rel{x} <- s, @x.decl := Rel{x},
    Rel{x} : ty !,
    Rel{x}.qname := qname, This{()} -> s, This{()} |-> d_this, d_this.qname := qname.

  RelRef[[ l@Label(x) ^ (s) : ty ]] :=
    Rel{x} -> s, Rel{x} |-> d | error $[Unresolved relation [x]] @x, @l.ref := Rel{x}, @x.decl := d,
    d : ty.

  RelRef[[ l@EOP() ^ (s) : PRED([SCOPE()]) ]].

  Constraint[[ CTellRel(rel, ts, st) ^ (s) ]] :=
    RelRef[[ rel ^ (s) : ty ]],
    tys is `inout-types` of ty,
    Map1T(Build)[[ ts ^ (s): tys ]],
    Build[[ st ^ (s) : SCOPE() ]].


/******************************************************************
 * Scope Graph                                                    *
 ******************************************************************/

  Signature[[ Namespaces(nss) ^ (s, s_syn) ]] :=
    Map2(DeclOrRule)[[ nss ^ (s, s_syn) ]].

  DeclOrRule[[ NsDecl(x, sorts) ^ (s, s_syn) ]] :=
    Ns{x} <- s, @x.decl := Ns{x},
    Map1T(SortRef)[[ sorts ^ (s) : tys ]],
    Ns{x} : NS(tys) !,
    Ns{x}.qname := qname, This{()} -> s, This{()} |-> d_this, d_this.qname := qname.

  Signature[[ NameResolution(params) ^ (s, s_syn) ]] :=
    Map2(DeclOrRule)[[ params ^ (s, s_syn) ]].

  DeclOrRule[[ Labels(lbls) ^ (s, s_syn) ]] :=
    Map1(EdgeDecl)[[ lbls ^ (s) ]].

  EdgeDecl[[ l@Label(x) ^ (s) ]] :=
    Edge{x} <- s, @x.decl := Edge{x},
    Edge{x}.qname := qname, This{()} -> s, This{()} |-> d_this, d_this.qname := qname.


  EdgeRef[[ l@Label(x) ^ (s) ]] :=
    Edge{x} -> s, Edge{x} |-> d | error $[Unresolved edge label [x]] @x, @l.ref := Edge{x}, @x.decl := d.

  EdgeRef[[ l@EOP() ^ (s) ]].


  LabelPair[[ LabelPair(lbl1, lbl2) ^ (s) ]] :=
    EdgeRef[[ lbl1 ^ (s) ]],
    EdgeRef[[ lbl2 ^ (s) ]].


  LabelRE[[ Empty() ^ (s) ]].

  LabelRE[[ Epsilon() ^ (s) ]].

  LabelRE[[ l@Label(_) ^ (s) ]] :=
    EdgeRef[[ l ^ (s) ]].

  LabelRE[[ Closure(re) ^ (s) ]] :=
    LabelRE[[ re ^ (s) ]].

  LabelRE[[ Neg(re) ^ (s) ]] :=
    LabelRE[[ re ^ (s) ]].

  LabelRE[[ Concat(re1, re2) ^ (s) ]] :=
    LabelRE[[ re1 ^ (s) ]],
    LabelRE[[ re2 ^ (s) ]].

  LabelRE[[ And(re1, re2) ^ (s) ]] :=
    LabelRE[[ re1 ^ (s) ]],
    LabelRE[[ re2 ^ (s) ]].

  LabelRE[[ Or(re1, re2) ^ (s) ]] :=
    LabelRE[[ re1 ^ (s) ]],
    LabelRE[[ re2 ^ (s) ]].


  Constraint[[ CTellEdge(t1, lbl, t2) ^ (s) ]] :=
    Build[[ t1 ^ (s) : SCOPE() ]],
    EdgeRef[[ lbl ^ (s) ]],
    Build[[ t2 ^ (s) : SCOPE() ]].

  Term(V,A)[[ StxOccurrence(ns, ts, pos) ^ (s) : OCCURRENCE() ]] :=
    Ns{ns} -> s, Ns{ns} |-> d | error $[Unresolved namespace [ns]] @ns, @ns.ref := Ns{ns}, @ns.decl := d,
    d : NS(tys),
    Map1T(Term(V,A))[[ ts ^ (s) : tys ]],
    OccurrencePos(Term(V,A))[[ pos ^ (s) : _ ]],
    Ns{ns}.qname := qname, This{()} -> s, This{()} |-> d_this, d_this.qname := qname.

  // only call this with a wildcard for ty
  OccurrencePos(T)[[ NoId() ^ (s) : AST_ID() ]].
  OccurrencePos(T)[[ WithId(t) ^ (s) : AST_ID() ]] :=
    TermOrId(T)[[ t ^ (s) : ty ]].

  Term(V,A)[[ PathEmpty(st) ^ (s) : PATH() ]] :=
    Term(V,A)[[ st ^ (s) : SCOPE() ]].

  Term(V,A)[[ PathStep(pt, lt, st) ^ (s) : PATH() ]] :=
    Term(V,A)[[ pt ^ (s) : PATH() ]],
    Term(V,A)[[ lt ^ (s) : LABEL() ]],
    Term(V,A)[[ st ^ (s) : SCOPE() ]].


/******************************************************************
 * Resolve & Query
 ******************************************************************/

  Constraint[[ CResolveQuery(rel, filter, min, st, pst, msg) ^ (s) ]] :=
    RelRef[[ rel ^ (s) : ty ]],
    inty is `in-type` of ty,
    Filter[[ filter ^ (s, PRED([inty])) ]],
    Min[[ min ^ (s, PRED([inty, inty])) ]],
    Build[[ st ^ (s) : SCOPE() ]],
    inoutty is `inout-type` of ty,
    Build[[ pst ^ (s) : LIST(TUPLE([PATH(), inoutty])) ]],
    Message(ErrorKind)[[ msg ^ (s) ]].

  Filter[[ Filter(re, lt) ^ (s, lty) ]] :=
    LabelRE[[ re ^ (s) ]],
    HoConstraint[[ lt ^ (s) : lty ]].

  Min[[ Min(ord, lt) ^ (s, lty) ]] :=
    LabelOrd[[ ord ^ (s) ]],
    HoConstraint[[ lt ^ (s) : lty ]].

  LabelOrd[[ [] ^ (s) ]].
  LabelOrd[[ prs@[_|_] ^ (s) ]] :=
    Map1(LabelPair)[[ prs ^ (s) ]].

/******************************************************************
 * Sorts & Constructors                                           *
 ******************************************************************/

  Signature[[ Sorts(sorts) ^ (s, s_syn) ]] :=
    Map2(SortDecl)[[ sorts ^ (s, s_syn) ]].

  SortDecl[[ SortDecl(x) ^ (s, s_syn) ]] :=
    Sort{x} <- s_syn, @x.decl := Sort{x},
    Sort{x} : SORT(Sort{x}) !!,
    Sort{x}.kind := SimpleSort(),
    Sort{x}.qname := qname, This{()} -> s, This{()} |-> d_this, d_this.qname := qname.

  SortDecl[[ SortAlias(x, sort) ^ (s, s_syn) ]] :=
    Sort{x} <- s_syn, @x.decl := Sort{x},
    Sort{x} : ty !!,
    Sort{x}.kind := SortAlias(),
    SortRef[[ sort ^ (s) : ty ]],
    Sort{x}.qname := qname, This{()} -> s, This{()} |-> d_this, d_this.qname := qname.

  Signature[[ Constructors(ctors) ^ (s, s_syn) ]] :=
    Map2(OpDecl)[[ ctors ^ (s, s_syn) ]].

  OpDecl[[ OpDecl(x, sort) ^ (s, s_syn) ]] :=
    x' is `op-name` of (x, sort),
    Op{x'@x} <- s_syn, @x.decl := Op{x'@x},
    Op{x'@x} : ty !!,
    Op{x'@x}.kind := UserOp(),
    OpSort[[ sort ^ (s) : ty ]],
    Op{x'@x}.qname := qname, This{()} -> s, This{()} |-> d_this, d_this.qname := qname.

  OpDecl[[ i@InjDecl(sort', sort) ^ (s, s_syn) ]] :=
    SortRef[[ sort' ^ (s) : ty' ]],
    RealSortRef[[ sort ^ (s) : ty ]],
    ty' <inj! ty,
    // implicit constructor declaration
    x' is `inj-op-name` of (sort', sort),
    Op{x'@i} <- s_syn,
    Op{x'@i} : OP([ty'], ty) !!,
    Op{x'@i}.kind := InjOp(),
    Op{x'@i}.qname := qname, This{()} -> s, This{()} |-> d_this, d_this.qname := qname.

  OpSort[[ ConstOp(sort) ^ (s) : OP([], ty) ]] :=
    RealSortRef[[ sort ^ (s) : ty ]].

  OpSort[[ ArrowOp(args, sort) ^ (s) : OP(tys, ty) ]] :=
    Map1T(SortRef)[[ args ^ (s) : tys ]],
    RealSortRef[[ sort ^ (s) : ty ]].

  RealSortRef[[ SimpleSort(x) ^ (s) : ty ]] :=
    Sort{x} -> s, Sort{x} |-> d | error $[Unresolved sort [x]] @x, @x.ref := Sort{x}, @x.decl := d,
    d : ty !,
    d.qname := qname, This{()} -> s, This{()} |-> d_this, d_this.qname := this_qname,
    d.kind := k, k != SortAlias() | error $[Cannot define constructors for sort alias [d]].

  SortRef[[ IntSort() ^ (_) : INT() ]].
  SortRef[[ StringSort() ^ (_) : STRING() ]].
  SortRef[[ OccurrenceSort() ^ (_) : OCCURRENCE() ]].
  SortRef[[ PathSort() ^ (_) : PATH() ]].
  SortRef[[ LabelSort() ^ (_) : LABEL() ]].
  SortRef[[ ScopeSort() ^ (_) : SCOPE() ]].
  SortRef[[ AstIdSort() ^ (_) : AST_ID() ]].
  SortRef[[ TupleSort(sorts) ^ (s) : TUPLE(tys) ]] :=
    Map1T(SortRef)[[ sorts ^ (s) : tys ]].
  SortRef[[ ListSort(sort) ^ (s) : LIST(ty) ]] :=
    SortRef[[ sort ^ (s) : ty ]].
  SortRef[[ SimpleSort(x) ^ (s) : ty ]] :=
    Sort{x} -> s, Sort{x} |-> d | error $[Unresolved sort [x]] @x, @x.ref := Sort{x}, @x.decl := d,
    d : ty,
    Sort{x}.qname := qname, This{()} -> s, This{()} |-> d_this, d_this.qname := qname.

  Type[[ SimpleType(sorts) ^ (s) : PRED(intys) ]] :=
    Map1T(SortRef)[[ sorts ^ (s) : intys ]].


/******************************************************************
 * Terms                                                          *
 ******************************************************************/

  Match[[ t ^ (s) : ty ]] :=
    Term(NonUniqueVarDecl,MatchCast)[[ t ^ (s) : ty ]].

  Build[[ t ^ (s) : ty ]] :=
    Term(VarRef,BuildCast)[[ t ^ (s) : ty ]].


  NonUniqueVarDecl[[ Var(x) ^ (s) : ty ]] :=
    Var{x @s} <- s, @x.decl := Var{x @s},
    Var{x @s} : ty !,
    Barrier{() @s} -> s, Barrier{() @s} |-> b, b ?=B=> s_b, Var{x @s}.barrier := s_b.

  NonUniqueVarDecl[[ Wld() ^ (_) : _ ]].

  VarDecl[[ Var(x) ^ (s) : ty ]] :=
    Var{x} <- s, @x.decl := Var{x},
    Var{x} : ty !,
    Barrier{()} -> s, Barrier{()} |-> b, b ?=B=> s_b, Var{x}.barrier := s_b.

  VarDecl[[ Wld() ^ (_) : _ ]].


  VarRef[[ Var(x) ^ (s) : ty ]] :=
    Var{x} -> s, Var{x} |-> d | error $[Unresolved variable [x]] @x, @x.ref := Var{x}, @x.decl := d,
    d : ty,
    Barrier{()} -> s, Barrier{()} |-> b, b ?=B=> s_b, Var{x}.barrier := s_b.

  VarRef[[ Wld() ^ (_) : _ ]].


  MatchCast(V,A)[[ Cast(t, sort) ^ (s) : ty ]] :=
    Term(V,A)[[ t ^ (s) : ty' ]],
    SortRef[[ sort ^ (s) : ty' ]],
    ty' <inj? ty.

  BuildCast(V,A)[[ Cast(t, sort) ^ (s) : ty ]] :=
    Term(V,A)[[ t ^ (s) : ty' ]],
    SortRef[[ sort ^ (s) : ty ]],
    ty' <inj? ty.


  Term(V,A)[[ v@Var(_) ^ (s) : ty ]] :=
    V[[ v ^ (s) : ty ]].
  Term(V,A)[[ Wld() ^ (_) : ty ]].
  Term(V,A)[[ List(ts) ^ (s): LIST(ty) ]] :=
    Map1t(Term(V,A))[[ ts ^ (s) : ty ]].
  Term(V,A)[[ ListTail(ts, t) ^ (s) : LIST(ty) ]] :=
    Map1t(Term(V,A))[[ ts ^ (s) : ty ]],
    Term(V,A)[[ t ^ (s) : LIST(ty) ]].
  Term(V,A)[[ Tuple(ts) ^ (s) : TUPLE(tys) ]] :=
    Map1T(Term(V,A))[[ ts ^ (s) : tys ]].
  Term(V,A)[[ Op(x, args) ^ (s) : ty ]] :=
    x' is `op-name` of (x, args),
    Op{x'@x} -> s, Op{x'@x} |-> d | error $[Unresolved constructor [x']] @x, @x.ref := Op{x'@x}, @x.decl := d,
    d : OP(tys, ty),
    Map1T(Term(V,A))[[ args ^ (s) : tys ]],
    d.kind := k, k <`same-name`? UserOp() | warning $[Use _:T syntax, instead of referring to the implicit constructor.],
    Op{x'@x}.qname := qname, This{()} -> s, This{()} |-> d_this, d_this.qname := qname.
  Term(V,A)[[ Str(_) ^ (_) : STRING() ]].
  Term(V,A)[[ Int(_) ^ (_) : INT() ]].
  Term(V,A)[[ As(v, t) ^ (s) : ty ]] :=
    V[[ v ^ (s) : ty ]],
    Term(V,A)[[ t ^ (s) : ty ]].
  Term(V,A)[[ cast@Cast(_, _) ^ (s) : ty ]] :=
    A(V,A)[[ cast ^ (s) : ty ]].


/******************************************************************
 * Arithmetic                                                     *
 ******************************************************************/

rules

  Constraint[[ CArith(ae1, op, ae2, msg) ^ (s) ]] :=
    ArithExp(VarRef,BuildCast)[[ ae1 ^ (s) : INT() ]],
    ArithExp(VarRef,BuildCast)[[ ae1 ^ (s) : INT() ]],
    Message(ErrorKind)[[ msg ^ (s) ]].


  ArithExp(V,A)[[ e@Int(_) ^ (s) : INT() ]] :=
    Term(V,A)[[ e ^ (s) : INT() ]].

  ArithExp(V,A)[[ e@Wld() ^ (s) : INT() ]] :=
    V[[ e ^ (s) : INT() ]].

  ArithExp(V,A)[[ e@Var(_) ^ (s) : INT() ]] :=
    V[[ e ^ (s) : INT() ]].

  ArithExp(V,A)[[ Add(e1, e2) ^ (s) : INT() ]] :=
    ArithExp(V,A)[[ e1 ^ (s) : INT() ]],
    ArithExp(V,A)[[ e2 ^ (s) : INT() ]].

  ArithExp(V,A)[[ Mul(e1, e2) ^ (s) : INT() ]] :=
    ArithExp(V,A)[[ e1 ^ (s) : INT() ]],
    ArithExp(V,A)[[ e2 ^ (s) : INT() ]].

  ArithExp(V,A)[[ Sub(e1, e2) ^ (s) : INT() ]] :=
    ArithExp(V,A)[[ e1 ^ (s) : INT() ]],
    ArithExp(V,A)[[ e2 ^ (s) : INT() ]].

  ArithExp(V,A)[[ Min(e1, e2) ^ (s) : INT() ]] :=
    ArithExp(V,A)[[ e1 ^ (s) : INT() ]],
    ArithExp(V,A)[[ e2 ^ (s) : INT() ]].

  ArithExp(V,A)[[ Max(e1, e2) ^ (s) : INT() ]] :=
    ArithExp(V,A)[[ e1 ^ (s) : INT() ]],
    ArithExp(V,A)[[ e2 ^ (s) : INT() ]].

  ArithExp(V,A)[[ Mod(e1, e2) ^ (s) : INT() ]] :=
    ArithExp(V,A)[[ e1 ^ (s) : INT() ]],
    ArithExp(V,A)[[ e2 ^ (s) : INT() ]].

  ArithExp(V,A)[[ Div(e1, e2) ^ (s) : INT() ]] :=
    ArithExp(V,A)[[ e1 ^ (s) : INT() ]],
    ArithExp(V,A)[[ e2 ^ (s) : INT() ]].


/******************************************************************
 * Messages                                                       *
 ******************************************************************/

  Message(K)[[ NoMessage() ^ (s) ]].

  Message(K)[[ Message(kind, content, origin) ^ (s) ]] :=
    K[[ kind ]],
    MessageContent[[ content ^ (s) ]],
    MessageOrigin[[ origin ^ (s) ]].


  MessageContent[[ Str(_) ^ (s) ]].

  MessageContent[[ Formatted(parts) ^ (s) ]] :=
    Map1(MessageContentPart)[[ parts ^ (s) ]].


  MessageContentPart[[ Text(_) ^ (s) ]].

  MessageContentPart[[ Term(t) ^ (s) ]] :=
    Build[[ t ^ (s) : _ ]].


  MessageOrigin[[ NoOrigin() ^ (s) ]].

  MessageOrigin[[ Origin(t) ^ (s) ]] :=
    Build[[ t ^ (s) : _ ]].


  ErrorKind[[ Error() ]].
  ErrorKind[[ Warning() ]] := false | error "Use 'try' for warnings.".
  ErrorKind[[ Note() ]] := false | error "Use 'try' for notes.".


  TryKind[[ Error() ]].
  TryKind[[ Warning() ]].
  TryKind[[ Note() ]].


/******************************************************************
 * Util                                                           *
 ******************************************************************/

rules

  Map1t(X)[[ [x|xs] ^ (s) : ty ]] :=
    X[[ x ^ (s) : ty ]],
    Map1t(X)[[ xs ^ (s) : ty ]].

  Map1t(X)[[ [] ^ (s) : _ ]].
