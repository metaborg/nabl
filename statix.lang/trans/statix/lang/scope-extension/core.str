module statix/lang/scope-extension/core

imports

  nabl2/api

  signatures/statix/lang/-
  statix/lang/-
  statix/lang/statics/-
  statix/lang/scope-extension/-

rules // collect extension constraints

  ext-constraints: r@Rule(_, C(x, ps), _) -> c*
    where r-ref := <ext-get-constraint-ref> x
        ; c-param* := <ext-params-constraints(|r-ref)> ps
        ; c-decl* := <ext-non-unique-decls-constraints> ps
        ; c* := <conc> (c-param*, c-decl*)

  ext-params-constraints(|r-ref): ps -> c*
    where ((v*, c1*), t*) := < add-indices
                             ; partition(ext-param-constraints(|r-ref))
                             ; (unzip;(id, concat), map(Snd))
                             > ps
        ; z* := <raw-diff> (<all-vars> t*, <all-vars> v*)
        ; c2* := <map(\ v@Var(z) -> [ExtIsCovered(ExtVar(<ext-get-var-decl> z), v)] \);concat> z*
        ; c* := <conc> (c1*, c2*)

  ext-param-constraints(|r-ref): (i, v@Var(y)) -> (v, c*)
    where v-decl := <ext-get-var-decl> y
        ; c* := [ ExtProvides(ExtParam(r-ref, i), ExtVar(v-decl), v)
                , ExtRequires(ExtParam(r-ref, i), ExtVar(v-decl), v)
                ]

  ext-param-constraints(|r-ref): (i, w@Wld()) -> (w, c*)
    where c* := [ ExtProvides(ExtParam(r-ref, i), ExtNone(), w)
                , ExtRequires(ExtParam(r-ref, i), ExtLit([]), w)
                ]

  ext-constraints: l@LLam(ps, body) -> c*
    where c* := <all-vars;map(\ Var(y) -> ExtIsCovered(ExtVar(<ext-get-var-decl> y), l) \)> ps


  ext-constraints: c@CTry(_, _) -> []

  ext-constraints: c@CExists(vs, _) -> c*
    where c* := <filter(is-var);map(\ v@Var(y) -> ExtIsCovered(ExtVar(<ext-get-var-decl> y), v) \)> vs

  ext-constraints: CNew(vs) -> c*
    where c* := <filter(is-var);map(\ v@Var(y) -> ExtProvides(ExtVar(<ext-get-var-ref> y), ExtAll(), v) \)> vs

  ext-constraints: CTellEdge(t, lbl, _) -> c*
    where t-ref := <ext-get-term-ref> t
        ; c* := [ExtRequires(t-ref, ExtLit([lbl]), t)]

  ext-constraints: CTellRel(lbl, _, t) -> c*
    where t-ref := <ext-get-term-ref> t
        ; c* := [ExtRequires(t-ref, ExtLit([lbl]), t)]


  ext-constraints: C(x, args, _) -> c*
    where r-ref := <ext-get-constraint-ref> x
        ; c* := <ext-args-constraints(|r-ref)> args

  ext-args-constraints(|r-ref): args -> c*
    where i_v* := <add-indices;filter((id, !(<id>, <ext-get-term-ref>)))> args
        ; c* := <map(ext-arg-constraints(|r-ref));concat> i_v*

  ext-arg-constraints(|r-ref): (i, (t, t-ref)) -> c*
    where c* := [ ExtProvides(t-ref, ExtParam(r-ref, i), t)
                , ExtRequires(t-ref, ExtParam(r-ref, i), t)
                ]


  ext-get-term-ref: Var(x) -> ExtVar(r-ref)
    where r-ref := <ext-get-var-ref> x

  ext-non-unique-decls-constraints: args -> c*
    with groups := <all-vars; group-by(?Var(<id>); strip-annos, \d -> (d, <?Var(<id>); ext-get-var-decl> d) \); map(Snd)> args
        ; c* := <mapconcat(\[(_, rep)|others] -> <mapconcat(ext-non-unique-decl-constraints(|rep))> others \)> groups

  ext-non-unique-decl-constraints(|rep): (t, d) -> c*
    with c* := [ ExtRequires(ExtVar(rep), ExtVar(d), t)
               , ExtRequires(ExtVar(d), ExtVar(rep), t)
               // FIXME Adding similar ExtProvides constraints again filters out these constraints.
               // How should we handle provides on non-uniquely declared variables?
               ]