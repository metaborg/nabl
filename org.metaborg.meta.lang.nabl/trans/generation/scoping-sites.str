module generation/scoping-sites

imports
  libstrc
  signatures/NameBindingLanguage-sig
  
  generation/-
  
rules 
  
  binding-to-scope-rules:
    BindingRule(pattern, _, clause*) -> [srule*, qrule*]
    where // scoped namespaces from scope clauses
       ns-term* := <filterconcat(?ScopeClause(<map(ns-to-term)>)); nonempty> clause*
     ; qrule*   := [QUERY_SCOPE_RULE(pattern, ns-term*)]
     ; scall*   := [SCOPE_CALL(ns-term*)]
    <+ qrule*   := [] 
     ; scall*   := []
    where // only for anonymous scopes
       <not(fetch-elem(?DefClause(Explicit(), _, _, _, _, _, _)))> clause*
     // congruences from defines ... in x, refers to ... in x, imports ... from x, imports ... into x 
     ; cong*  := <filterconcat(to-scope-cong(|pattern))> clause*
     ; srule* := [SCOPE_RULE(pattern, <nonempty; to-seq> [scall*, cong*])]
    <+ srule* := []
       
  to-scope-cong(|pattern):
    _ -> <filterconcat(to-scope-cong(|pattern)); nonempty> part*
    where
      ?RefClause(part*)
    + ?ImportClause(part*)
    
  to-scope-cong(|pattern):
    _ -> [cong1*, cong2*]
    where
       ?DefClause(_, _, ns, term, _, into, _)
     ; ns* := [ns]
    <+ ?RefClausePart(_, ns, _, _, from, _)
     ; ns* := [ns]
    <+ ?SingleImport(_, ns, _, _, from, _, into, _)
     ; ns* := [ns]
    <+ ?WildcardImport(import*, _, from, into, _)
     ; ns* := <map(?Restricted(_, <id>)); make-set> import*
    with
       ns-term* := <map(ns-to-term)> ns*
    with
       repl   := (<collect-one(?RefScope(<id>))> from, SCOPE_CALL(ns-term*))
     ; cong1* := [<add-force-origins; replace-all-id(|[repl])> pattern]
    <+ cong1* := []
    with
    	 scope* := <?DefScopes(<filter(?DefScope(<id>))>); nonempty> into
     ; repl*  := <map(!(<id>, SCOPE_CALL(ns-term*)))> scope*
     ; cong2* := [<add-force-origins; replace-all-id(|repl*)> pattern]
    <+ cong2* := []
 
 /**
  * scope-to-term transforms NaBL scope definition into a Stratego term for consumption by the NaBL runtime
  *
  * NaBL input     : defines   ... in current scope
  * NaBL input     : refers to ... in current scope
  * Stratego output: Current()
  * 
  * NaBL input      : defines ... in subsequent scope
  * Stratego output : [Subsequent()]
  *
  * NaBL input      : defines ... in ctx1, ..., ctxn
  * Stratego output : [DefScope(ctx1), ..., DefScope(ctxn)]
  *
  * NaBL input      : defines ... in subsequent scope, ctx1, ..., ctxn
  * Stratego output : [Subsequent(), DefScope(ctx1), ..., DefScope(ctxn)]
  *
  * NaBL input      : refers to ... in enclosing Namespace
  * Stratego output : Enclosing(NablNsNamespace())
  *
  * NaBL input      : refers to ... in ctx
  * Stratego output : scope-i
  *
  */

  scope-to-term: Current()      -> TERM("Current")
  scope-to-term: Subsequent()   -> TERM("Subsequent")
  scope-to-term: DefScopes(ds*) -> LIST(<map(scope-to-term)> ds*)
  scope-to-term: DefScope(term) -> TERM("DefScope", [term])
  scope-to-term: Enclosing(ns)  -> TERM("Enclosing", [<ns-to-term> ns])
  scope-to-term: RefScope(term) -> TERM("RefScope", [term])
    
  scope-to-term:
    Context(disamb, ns, term, prop*, ctx) -> TERM("Context", [ns-term, term, LIST(prop-term*), ctx-term])
    where
      ns-term    := <ns-to-term> ns
    ; prop-term* := <map(prop-to-str)> prop*
    ; ctx-term   := <scope-to-term> ctx
  
overlays
  
  QUERY_SCOPE_RULE(pattern, ns) = SDefNoArgs("nabl-get-scope", Seq(Match(pattern), Build(LIST(ns))))
  
  SCOPE_RULE(pattern, body) = SDefNoArgs("nabl-scoping-site", Seq(Match(pattern), body))
  SCOPE_CALL(ns)            = CallT(SVar("nabl-scope"), [], [LIST(ns)])
 
   
