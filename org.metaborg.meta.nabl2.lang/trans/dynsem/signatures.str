module dynsem/signatures

imports

  libspoofax/core/language
  dynsem/-
  util/src-gen
  generation/defaults
  signatures/signatures/-

rules

  generate-dynsem-signatures(|path, project-path, langname):
    ast@Module(name,_) -> [(filename, source)]
    with filename := <create-src-gen(|project-path, "nabl2", ".ds")> name;
         ds-ast := <module-to-ds(|langname)> ast;
         source := <ds-pp-module> ds-ast

  module-to-ds(|langname):
    ast@Module(name,_) -> Module(name,[
      Imports([
        Import("nabl2/runtime/interpreter/scopegraph-api"),
        Import($[ds-signatures/[langname]-sig])
      ]),
      Signatures([
        NativeOperators([
          params-op*,
          type-op*
        ]),
        ArrowDeclarations([
          params-arrow*,
          type-arrow*
        ]),
        Constructors([
          type-con*
        ])
      ]),
      Rules([
        params-rule*,
        type-rule*
      ])
    ])
    with (params-op*, params-arrow*, params-rule*) := <collect-om(cgen-sig-to-params-ds);unzip3> ast;
         (type-op*, type-arrow*, type-rule*) := <collect-om(cgen-sig-to-type-ds);unzip3> ast;
         type-con* := <collect-om(type-sig-to-ds)> ast

  cgen-sig-to-params-ds: CGenSig(Name(name), sort, param*, _) -> <native-op-ds(|name)>  (<sort-to-ds> sort, "paramsOf", <param-sorts-to-ds> param*)

  cgen-sig-to-type-ds: CGenSig(Name(name), sort, _, Type()) -> <native-op-ds(|name)>  (<sort-to-ds> sort, "typeOf", SimpleSort("Type"))

  unzip3 = ?[(x,y,z)|xyz*] < <unzip3> xyz* => (x*,y*,z*); !([x|x*],[y|y*],[z|z*]) + !([],[],[])

  native-op-ds(|rule-name): (src-sort, base-name, target-sort) -> (op, arrow, rule)
    with op-name := <cgen-op-name(|rule-name, src-sort)> base-name;
         op := NativeOpDecl(op-name, [src-sort], target-sort);
         arrow-name := <cgen-arrow-name(|rule-name)> base-name;
         arrow := ArrowDecl(NoArrowROs(),src-sort,NoArrowRWs(),arrow-name,target-sort,NoArrowRWs());
         rule := Axiom(Relation(NoReads(),Source(Cast(VarRef("t"),src-sort)),NamedDynamic(arrow-name),Target(Con(op-name, [VarRef("t")]))))

  cgen-op-name(|rule-name,sort): base-name -> op-name
    with sort-name := <ds-sort-to-string> sort;
         if default-rule-name => rule-name then
           op-name := $[nabl2_[base-name][sort-name]]
         else
           op-name := $[nabl2_[rule-name]_[base-name][sort-name]]
         end

  cgen-arrow-name(|rule-name): base-name -> arrow-name
    with if default-rule-name => rule-name then
           arrow-name := $[[base-name]]
         else
           arrow-name := $[[rule-name]_[base-name]]
         end

  type-sig-to-ds: TypeSig(name, []) -> NullaryConsDecl(name, SimpleSort("Type"), NoAnnos())
  type-sig-to-ds: TypeSig(name, arg*) -> ConsDecl(name, <map(Snd;sort-to-ds)> arg*, SimpleSort("Type"), NoAnnos())

  param-sorts-to-ds: [sort] -> <sort-to-ds> sort
  param-sorts-to-ds: sort* -> <sort-to-ds> TupleSort(sort*)

  sort-to-ds: SortNoArgs(name) -> SimpleSort(name)
  sort-to-ds: Sort("List", [arg]) -> ListSort(<sort-to-ds> arg)
  sort-to-ds: TupleSort(arg*) -> TupleSort(<map(sort-to-ds)> arg*)
  sort-to-ds: Sort(name, arg*) -> <fatal-err(|"Not supported in DynSem")>
  sort-to-ds: ScopeSort() -> SimpleSort("Scope")
  sort-to-ds: TypeSort() -> SimpleSort("Type")
