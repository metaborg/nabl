module generation/signatures/nameresolution

imports

  libstrc
  nabl2shared
  generation/-
  signatures/signatures/-
  util/stratego

rules

  signature-to-str:
    NameResolution(_) -> []

  sections-to-nameresolution-str:
    section* -> [Rules([labelstr*, orderstr*, wfstr*])]
    with param* := <filter(?Signature(<id>));concat;
                    filter(?NameResolution(<id>));concat> section*
    with if label*@[_|_] := <filter(?Labels(<id>));concat> param* then
           labelterm*    := <map(explode(injection-to-term))> label*;
           labelstr*     := [ |[ custom-labels = !~List(labelterm*) ]| ]
         else
           labelstr*    := []
         end
    with order* := <filter(?Order(<id>));concat> param*;
         orderstr* := <map(label-order-to-str)> order*
    with if wf := <getfirst(?WF(<id>))> param* then
           wfterm := <explode(injection-to-term)> wf;
           wfstr* := [ |[ custom-labels-wf = !~wfterm  ]| ]
         else
           wfstr* := []
         end
 
  label-order-to-str: Lt(l1, l2) -> |[ custom-label-lt = ?(~l1',~l2') ]|
    with l1' := <explode(injection-to-term)> l1
       ; l2' := <explode(injection-to-term)> l2
