module rules/CGen

imports
 
  nabl2/shared/common/CTerms
  nabl2/shared/common/Identifiers
  nabl2/shared/constraints/Base

  common/Identifiers
  common/Terms
  rules/Rules

template options

  keyword -/- [a-zA-Z0-9\']

context-free syntax

  Rule.CGenInitRule = <
    init <CGenParamsTerm> <CGenTypeTerm> :=
        <{RuleClause ","}+>.
  >

  Rule.CGenMatchRuleNoConstraint = <
    <RuleName> [[ <Pattern> <CGenParamsPattern> <CGenTypePattern> ]] .
  >

  Rule.CGenMatchRule = <
    <RuleName> [[ <Pattern> <CGenParamsPattern> <CGenTypePattern> ]] :=
        <{RuleClause ","}+>.
  >

  RuleClause = PrimitiveConstraint

  RuleClause = <(<Constraint>)>

  RuleClause.CGenRecurse = <<RuleName> [[ <Var> <CGenParamsTerm> <CGenTypeTerm> ]]>

  RuleClause.NewScopes   = <new <{CVar " "}+>>

context-free syntax

  CGenParamsTerm.Tuple    = <^ (<{CTerm ","}*>)>

  CGenParamsPattern.Tuple = <^ (<{VarOrWld ","}*>)>

  CGenTypeTerm.NoType     =
  CGenTypeTerm.Type       = <: <CTerm>>

  CGenTypePattern.NoType  =
  CGenTypePattern.Type    = <: <CTerm>>

lexical syntax

  Keyword = "new"

 