module statix/runtime/analysis

imports

  libstratego-lib

  libspoofax/analysis/constraint
  libspoofax/core/language
  libspoofax/resource/path

  signatures/statix/runtime/-
  pp/statix/runtime/-

  statix/runtime/ast
  statix/runtime/constraints
  statix/runtime/terms
  statix/runtime/pp

signature
  constructors

    ProjectAnalysis : Scope * SolverResult * Constraint * Analysis -> AnalysisResult
    FileAnalysis    : Constraint -> AnalysisResult

    ProjectAnalysisNew : Constraint * Analysis -> AnalysisResult
    FileAnalysisNew    : Constraint * Analysis -> AnalysisResult

    FileSpec : List(String) * List(Label) * List(Label) * Label * List(Rule) * List(ID, Integer, Label) -> Spec
    Spec     : List(Label) * List(Label) * Label * List(Rule) * List(ID, Integer, Label) -> Spec
    Solution : List(Var, Term) * SolverResult -> Solution

    EOP : Label // duplicate from statix.lang

rules // single file analysis

  stx--editor-analyze(pre, post|spec-name, init-constraint):
      AnalyzeSingle(change*, progress, cancel) -> AnalysisResult(result*)
    with spec  := <stx--language-spec-by-name> spec-name
       ; level := <stx--log-level>
       ; c-name := <stx--mk-qualified-name(|spec-name)> init-constraint
    with
      resource-ast* := <filter((id, ?Added(<id>) + ?Changed(<id>, _)))> change*
    ; resource-ast'* := <map(\ (resource, ast) -> (resource, <pre;stx--index-ast(|resource)> ast) \)> resource-ast*
    ; C* := <map(\ (resource, ast') -> C(c-name, [<stx--explode> ast'], NoMessage()) \)> resource-ast'*
    ; solve-result* := <stx--solve-constraint(|spec, level, progress, cancel)> C*
    ; result* := <zip({ ast'', file-analysis, error*, warning*, note*: \
        ((resource, ast'), Solution(_, a)) ->
            (resource, Full(ast'', file-analysis, error*, warning*, note*))
          with (error*, warning*, note*) := <stx--delays-as-errors;stx--extract-messages> a
             ; ast'' := <post> ast'
             ; file-analysis := FileAnalysis(a)
      \})> (resource-ast'*, solve-result*)

  stx--editor-analyze(pre, post|spec-name, init-constraint):
      AnalyzeMulti(_, _, _, _) -> <fatal-err-msg(|"Single-file `stx-editor-analyze_1_2` called for multi-file analysis. Use `stx-editor-analyze_1_3` instead.")>

rules // single file elaboration

  stx--editor-elaborate(pre, post|spec-name, init-constraint):
      AnalyzeSingle(change*, progress, cancel) -> AnalysisResult(result*)
    with spec  := <stx--language-spec-by-name> spec-name
       ; level := <stx--log-level>
       ; c-name := <stx--mk-qualified-name(|spec-name)> init-constraint
    with
      resource-ast* := <filter((id, ?Added(<id>) + ?Changed(<id>, _)))> change*
    ; resource-ast'* := <map(\ (resource, ast) -> (resource, <pre;stx--index-ast(|resource)> ast) \)> resource-ast*
    ; V := Var("out")
    ; C* := <map(\ (resource, ast') -> <stx--exists>([V], C(c-name, [<stx--explode> ast', V], NoMessage())) \)> resource-ast'*
    ; solve-result* := <stx--solve-constraint(|spec, level, progress, cancel)> C*
    ; result* := <zip({ ast'', file-analysis, error*, warning*, note*: \
        ((resource, ast'), Solution(subst, a)) ->
            (resource, Full(ast'', file-analysis, error*, warning*, note*))
          with (error*, warning*, note*) := <stx--delays-as-errors;stx--extract-messages> a
          with ast'' := <substitute(stx--is-var);stx--implode;post> (subst, V)
             ; file-analysis := FileAnalysis(a)
      \})> (resource-ast'*, solve-result*)

rules // multi file analysis (new)

  stx--editor-analyze-new(pre, post|spec-name, project-constraint, file-constraint):
      AnalyzeMulti((project-resource, project-change), file-change*, progress, cancel) -> AnalysisResult(result*)
    with spec  := <stx--language-spec-by-name> spec-name
       ; level := <stx--log-level>
       ; file-c-name := <stx--mk-qualified-name(|spec-name)> file-constraint
       ; proj-c-name := <stx--mk-qualified-name(|spec-name)> project-constraint
       ; global-scope := Scope("", "0") // also hardcoded in STX_solve_multi
    with
      //
      // 1. Build project task
      //
      switch !project-change
        case ?Added(project-ast) + ?Changed(project-ast, _):
          project-C := C(proj-c-name, <map(stx--explode)> [global-scope], NoMessage())
        case ?Cached(ProjectAnalysisNew(project-C, _)):
          project-ast := ()
        otherwise:
          fatal-err(|"Unexpected change on project")
      end
    ; project-task := (project-resource, project-C)
      //
      // 2. Build file tasks
      //
    ; changed-file-ast*  := <filter((id, ?Added(<id>) + ?Changed(<id>, _)))> file-change*
    ; changed-file-ast'* := <map(\ (resource, ast) -> (resource, <pre;stx--index-ast(|resource)> ast) \)> changed-file-ast*
    ; changed-file-task* := <map(\ (resource, ast') -> (resource, C(file-c-name, <map(stx--explode)> [global-scope, ast'], NoMessage())) \)> changed-file-ast'*
    ; cached-file-task*  := <filter(\ (resource, Cached(FileAnalysisNew(file-C, _))) -> (resource, file-C) \)> file-change*
    ; file-task* := [changed-file-task*, cached-file-task*]
      //
      // 3. Solve all tasks
      //
    ; task*          := [project-task, file-task*]
    ; changed-ast*   := [(project-resource, project-ast), changed-file-ast'*]
    ; analysis*      := <stx--solve-multi-new(|spec, level, progress, cancel)> task*
    ; project-result := < stx--mk-result(\ (C, a) -> ProjectAnalysisNew(C, a) \|changed-ast*, task*, analysis*)
                        ; \ (resource, result) -> (resource, result) \
                        > (project-resource, project-change)
    ; file-result*   := <map( stx--mk-result(\ (C, a) -> FileAnalysisNew(C, a) \|changed-ast*, task*, analysis*)
                            ; \ (resource, result) -> (resource, result) \
                            )> file-change*
    ; result* := [project-result|file-result*]
      //
      // 4. Compute scope graph diff
      //
    ; if Cached(ProjectAnalysisNew(_, project-analysis')) := project-change then
        project-analysis := <lookup> (project-resource, analysis*)
      ; diff := <stx--diff-scopegraphs(|global-scope)> (project-analysis, project-analysis')
      ; <pp-Statix-string(prettyprint-Statix-Stx-Diff);debug> diff
      end

  // @param spec  : Spec
  // @param level : LogLevel
  // @type [(String, Constraint)] -> [(String, SolverResult)]
  stx--solve-multi-new(|spec, level, progress, cancel) =
    prim("STX_solve_multi", spec, level, progress, cancel)

  stx--mk-result(mk-analysis|changed-ast*, task*, state*): (resource, change) -> (resource, result)
    with C := <lookup> (resource, task*)
       ; state := <lookup> (resource, state*)
       ; analysis := <mk-analysis> (C, state)
       ; (error*, warning*, note*) := <stx--delays-as-errors; stx--extract-messages> state
       ; switch !change
           case ?Added(_) + ?Changed(_, _):
             ast    := <lookup> (resource, changed-ast*)
           ; result := Full(ast, analysis, error*, warning*, note*)
           case ?Cached(_):
             result := Update(analysis, error*, warning*, note*)
         end


rules // multi file analysis

  stx--editor-analyze(pre, post|spec-name, project-constraint, file-constraint):
      AnalyzeMulti((project-resource, project-change), change*, progress, cancel) -> AnalysisResult(result*)
    with spec  := <stx--language-spec-by-name> spec-name
       ; level := <stx--log-level>
       ; file-c-name := <stx--mk-qualified-name(|spec-name)> file-constraint
       ; proj-c-name := <stx--mk-qualified-name(|spec-name)> project-constraint
    with
      switch !project-change
        case ?Added(_) + ?Changed(_, _):
          //
          // 1. create global scope
          //
          global-scope-var := Var("s")
        ; Solution(global-scope-subst, global-scope-state) :=
              <stx--solve-constraint(|spec, level, progress, cancel)> <stx--exists>([global-scope-var], CNew([global-scope-var]))
        ; global-scope := <lookup> (global-scope-var, global-scope-subst)
          //
          // 2. partially solve project constraint
          //
        ; project-C := C(proj-c-name, <map(stx--explode)> [global-scope], NoMessage())
        ; [initial-project-state] := <stx--solve-multi-file(|spec, global-scope-state, level, progress, cancel)> [(project-resource, project-C)]
        case ?Cached(ProjectAnalysis(global-scope, global-scope-state, initial-project-state, _)):
          id
        otherwise:
          fatal-err(|"Unexpected change on project")
      end
      //
      // 3. Partially solve file constraints
      //
    ; resource-ast* := <filter((id, ?Added(<id>) + ?Changed(<id>, _)))> change*
    ; resource-ast'* := <map(\ (resource, ast) -> (resource, <pre;stx--index-ast(|resource)> ast) \)> resource-ast*
    ; file-C* := <map(\ (resource, ast') -> (resource, C(file-c-name, <map(stx--explode)> [global-scope, ast'], NoMessage())) \)> resource-ast'*
    ; file-state* := <stx--solve-multi-file(|spec, global-scope-state, level, progress, cancel)> file-C*
    ; file-result* := <zip({ file-state', ast'', file-analysis: \
        ((resource, ast'), file-state) ->
            (resource, Full(ast'', file-analysis, [], [], []))
          with ast'' := <post> ast'
             ; file-analysis := FileAnalysis(file-state)
      \})> (resource-ast'*, file-state*)
      //
      // 4. Combine and solve all partial solution
      //
    ; cached-state* := <filter(?(_, Cached(FileAnalysis(<id>))))> change*
    ; final-project-state := <stx--solve-multi-project(|spec, initial-project-state, level, progress, cancel)> [file-state*, cached-state*]
    ; (error*, warning*, note*) := <stx--delays-as-errors;stx--extract-messages> final-project-state
    ; project-analysis := ProjectAnalysis(global-scope, global-scope-state, initial-project-state, final-project-state)
    ; switch !project-change
        case ?Added(project-ast) + ?Changed(project-ast, _):
          project-result := Full(project-ast, project-analysis, error*, warning*, note*)
        case ?Cached(_):
          project-result := Update(project-analysis, error*, warning*, note*)
      end
    ; result* := [(project-resource, project-result)|file-result*]
      //
      // 5. Compute scope graph diff
      //
    ; if Cached(ProjectAnalysis(_, _, _, final-project-state')) := project-change then
        diff := <stx--diff-scopegraphs(|global-scope)> (final-project-state, final-project-state')
      ; <pp-Statix-string(prettyprint-Statix-Stx-Diff);debug> diff
      end

  stx--editor-analyze(pre, post|spec-name, init-constraint):
      AnalyzeSingle(_, _, _) -> <fatal-err-msg(|"Multi-file `stx-editor-analyze_1_3` called for single-file analysis. Use `stx-editor-analyze_1_2` instead.")>

  // @param state : SolverResult
  // @param level : LogLevel
  // @type [(String, Constraint)] -> [SolverResult]
  stx--solve-multi-file(|spec, state, level, progress, cancel) = prim("STX_solve_multi_file", spec, state, level, progress, cancel)

  // @param state : SolverResult
  // @param level : LogLevel
  // @type [SolverResult] -> SolverResult
  stx--solve-multi-project(|spec, state, level, progress, cancel) = prim("STX_solve_multi_project", spec, state, level, progress, cancel)

  // @type SolverResult -> SolverResult
  stx--delays-as-errors = prim("STX_delays_as_errors")

  // @type SolverResult -> (SolverResult, [Error], [Warning], [Note])
  stx--extract-messages = prim("STX_extract_messages")

rules

  stx--evaluate(|spec-name, constraint):
      in* -> (out, s, error*, warning*, note*)
    with spec  := <stx--language-spec-by-name> spec-name
       ; c-name := <stx--mk-qualified-name(|spec-name)> constraint
       ; T*    := <map(stx--explode)> in*
       ; V     := Var("out")
       ; C     := <stx--exists>([V], C(c-name, [T*, V], NoMessage()))
       ; level := <stx--log-level>
       ; s@Solution(subst, a) := <stx--solve-constraint(|spec, level, (), ())> C
       ; (error*, warning*, note*) := <stx--delays-as-errors;stx--extract-messages> a
       ; out := <substitute(stx--is-var);stx--implode> (subst, V)

rules

  stx--log-level = stx--is-debug-custom-enabled < !"Info" + !"None"

  stx--language-spec-by-name =
    MkSingleton
  ; language-resources(stx--module-path, stx--spec-imports)
  ; map(Snd)
  ; stx--merge-spec-aterms

  stx--local-spec-from-spec =
    ?spec
  ; stx--spec-imports
  ; project-resources(stx--module-path, stx--spec-imports)
  ; map(Snd)
  ; ![spec|<id>]
  ; stx--merge-spec-aterms


  stx--spec-imports = ?FileSpec(<id>, _, _, _, _, _)


  stx--mk-qualified-name(|""): name -> name

  stx--mk-qualified-name(|qname): name -> name'
    with if <elem> ("!", <explode-string> name) then
           name' := name
         else
           name' := $[[qname]![name]]
         end


  stx--merge-spec-aterms = foldr(stx--merge-spec-aterms-init, stx--merge-spec-aterms-step)

  stx--merge-spec-aterms-init = !Spec([], [], EOP(), [], [])

  stx--merge-spec-aterms-step: (spec1, spec) -> spec'
    with FileSpec(_, edge1*, data1*, eop, rule1*, ext1*) := spec1
       ; Spec(edge*, data*, eop, rule*, ext*) := spec
       ; spec' := Spec( <union> (edge1*, edge*)
                      , <union> (data1*, data*)
                      , eop
                      , <conc> (rule1*, rule*)
                      , <union> (ext1*, ext*)
                      )

rules

  stx--module-path: name -> filename
    with filename := <stx--src-gen-file(|"statix", ".spec.aterm")> name

  stx--src-gen-file(|sub-dir, ext): name -> filename
    with filename := $[[<stx--src-gen-dir(|sub-dir)>]/[name][ext]]

  stx--src-gen-dir(|sub-dir): _ -> dirname
    with dirname := <project-srcgen-dir> sub-dir


rules

  // solve-constraint(|Spec, LogLevel) : ([Var], Constraint) -> Solution
  stx--solve-constraint(|spec, level) = stx--solve-constraint(|spec, level, (), ())

  // solve-constraint(|Spec, LogLevel, Progress, Cancel) : ([Var], Constraint) -> Solution
  stx--solve-constraint(|spec, level, progress, cancel) = prim("STX_solve_constraint", spec, level, progress, cancel)

rules

  stx--get-resource-analysis =
    get-resource-constraint-analysis
  ; ( ?FileAnalysis(<id>)
    + ?ProjectAnalysis(_, _, _, <id>)
    )

  stx--get-ast-analysis =
    stx--get-ast-resource
  ; stx--get-resource-analysis

  stx--is-debug-custom-enabled =
    prim("SG_is_debug_custom_enabled")

rules

  /**
   * @param a : Analysis
   * @type : (Scope * Label) -> [Term]
   */
  stx--get-scopegraph-data(|a) =
    prim("STX_get_scopegraph_data", a)

  /**
   * @param a : Analysis
   * @type : (Scope * Label) -> [Scope]
   */
  stx--get-scopegraph-edges(|a) =
    prim("STX_get_scopegraph_edges", a)

  /**
   * @type : Analysis -> Analysis
   */
  stx--is-analysis =
    prim("STX_is_analysis")

  /**
   * @type : Analysis -> ScopeGraph
   */
  stx--debug-scopegraph =
    prim("STX_debug_scopegraph")

  stx--diff-scopegraphs(|s0) =
    prim("STX_diff_scopegraphs",s0)

  stx--get-occurrence-ns: StxOccurrence(ns, _, _) -> ns

  stx--get-occurrence-terms: StxOccurrence(_, ts, _) -> ts
