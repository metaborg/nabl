module nabl2/runtime/analysis/main

imports

  nabl2/api
 
  signatures/nabl2/shared/common/-
  signatures/nabl2/shared/constraints/-
  nabl2/shared/-

  signatures/nabl2/runtime/analysis/-
  signatures/nabl2/runtime/common/-
  nabl2/runtime/analysis/-
  nabl2/runtime/prelude/-
  nabl2/runtime/transform/-

signature
  constructors
    WithCancelProgress : Term * Cancel * Progress -> WithCancelProgress

rules

  nabl2--solve-constraint(|config, cancel, progress) = <prim("SG_solve_single_constraint", config)> WithCancelProgress(<id>, cancel, progress)

  nabl2--analyze-ast(|resource): ast -> (ast', analysis, error*, warning*, note*)
    with config := <nabl2--analysis-config>;
         resource' := <nabl2--set-ast-index(|TermIndex(resource, 0))> resource;
         nabl2--collection-info-msg(|$[Initializing [resource]]);
         (params, C1) := <nabl2--generate-constraint-init(|resource)> resource';
         ast' := <nabl2--index-ast(|resource)> ast;
         nabl2--collection-info-msg(|$[Generating constraints [resource]]);
         C2 := <nabl2--generate-constraint-file(|resource, params)> ast';
         nabl2--collection-info-msg(|$[Solving constraints [resource]]);
         (analysis, error*, warning*, note*) := <nabl2--solve-constraint(|config, (), ())> CConj(C1, C2);
         nabl2--collection-info-msg(|$[Finished analyzing [resource]])

rules // single file analysis

  nabl2--analyze(pre) = constraint-analysis-compat(nabl2--analyze-compat(pre))

  nabl2--analyze-compat(pre):
      AnalyzeUnit(resource, ast, cancel, progress) -> UnitResult(ast', a', error*, warning*, note*)
    with config := <nabl2--analysis-config>;
         resource' := <nabl2--set-ast-index(|TermIndex(resource, 0))> resource;
         nabl2--collection-info-msg(|$[Initializing [resource]]);
         (params, initial-C) := <nabl2--generate-constraint-init(|resource)> resource';
         initial-custom := <nabl2-custom-analysis-init-hook <+ !None()> (resource, resource');
         nabl2--collection-info-msg(|$[Preprocessing AST [resource]]);
         ast' := <pre;nabl2--index-ast(|resource)> ast;
         nabl2--collection-info-msg(|$[Generating constraints [resource]]);
         file-C := <nabl2--generate-constraint-file(|resource, params)> ast';
         nabl2--collection-info-msg(|$[Solving constraints [resource]]);
         (a, constraint-error*, constraint-warning*, constraint-note*) :=
             <nabl2--solve-constraint(|config, cancel, progress)> CConj(initial-C, file-C);
         file-custom := <nabl2-custom-analysis-unit-hook <+ nabl2-custom-analysis-unit-hook2(|a) <+ !None()> (resource, ast', initial-custom);
         if (custom-error*, custom-warning*, custom-note*, final-custom) :=
                <nabl2-custom-analysis-final-hook(|a)> (resource, initial-custom, [file-custom]) then
           a' :=  <nabl2--set-custom-analysis(|a)> final-custom
         else
           (custom-error*, custom-warning*, custom-note*, a') := ([], [], [], a)
         end;
         nabl2--collection-info-msg(|$[Finished analyzing [resource]]);
         error*   := [constraint-error*, custom-error*];
         warning* := [constraint-warning*, custom-warning*];
         note*    := [constraint-note*, custom-note*]

rules // multi file analysis

  nabl2--analyze-compat(pre):
      AnalyzeInitial(resource, cancel, progress) -> InitialResult(initial-result)
    with config := <nabl2--analysis-config>;
         resource' := <nabl2--set-ast-index(|TermIndex(resource, 0))> resource;
         nabl2--collection-info-msg(|$[Initializing [resource]]);
         (params, C) := <nabl2--generate-constraint-init(|resource)> resource';
         initial-analysis := <prim("SG_solve_multi_initial_constraint", config)> WithCancelProgress((params, C), cancel, progress);
         if initial-custom := <nabl2-custom-analysis-init-hook> (resource, resource') then
           initial-analysis' :=  <nabl2--set-custom-analysis(|initial-analysis)> initial-custom
         else
           initial-analysis' := initial-analysis
         end;
         initial-result := (params, initial-analysis')

  nabl2--analyze-compat(pre):
      AnalyzeUnit(resource, ast, initial-result, cancel, progress) -> UnitResult(ast', unit-analysis')
    with (params, initial-analysis) := initial-result;
         initial-custom := <nabl2--get-custom-analysis <+ !None()> initial-analysis
    with nabl2--collection-info-msg(|$[Preprocessing AST [resource]]);
         ast' := <pre;nabl2--index-ast(|resource)> ast;
         nabl2--collection-info-msg(|$[Generating constraints [resource]]);
         C := <nabl2--generate-constraint-file(|resource, params)> ast';
         unit-analysis := <prim("SG_solve_multi_unit_constraint")> WithCancelProgress((initial-analysis, C), cancel, progress);
         if unit-custom := <nabl2-custom-analysis-unit-hook <+ nabl2-custom-analysis-unit-hook2(|unit-analysis)> (resource, ast', initial-custom) then
           unit-analysis' :=  <nabl2--set-custom-analysis(|unit-analysis)> unit-custom
         else
           unit-analysis' := unit-analysis
         end

  nabl2--analyze-compat(pre):
      AnalyzeFinal(resource, initial-result, unit-analysis*, cancel, progress) -> FinalResult(final-analysis', error*, warning*, note*)
    with (params, initial-analysis) := initial-result;
         initial-custom := <nabl2--get-custom-analysis <+ !None()> initial-analysis;
         unit-custom* := <filter(nabl2--get-custom-analysis)> unit-analysis*
    with nabl2--collection-info-msg(|$[Solving constraints [resource]]);
         (final-analysis, constraint-error*, constraint-warning*, constraint-note*) :=
             <prim("SG_solve_multi_final_constraint")> WithCancelProgress((initial-analysis, unit-analysis*), cancel, progress);
         if (custom-error*, custom-warning*, custom-note*, final-custom) :=
               <nabl2-custom-analysis-final-hook(|final-analysis)> (resource, initial-custom, unit-custom*) then
           final-analysis' :=  <nabl2--set-custom-analysis(|final-analysis)> final-custom
         else
           (custom-error*, custom-warning*, custom-note*, final-analysis') :=
               ([], [], [], final-analysis)
         end;
         nabl2--collection-info-msg(|$[Finished analyzing [resource]]);
         error*   := [constraint-error*, custom-error*];
         warning* := [constraint-warning*, custom-warning*];
         note*    := [constraint-note*, custom-note*]

rules

  nabl2--analysis-config: _ -> config
    with labels := <nabl2--labels>;
         wf := <nabl2--labels-wf>;
         order := <nabl2--label-order>;
         strategy := <nabl2--strategy>;
         path-relevance := <nabl2--path-relevance>;
         res-params := (labels,wf,order,strategy,path-relevance);
         rel* := <nabl2--relations>;
         fun* := <nabl2--functions>;
         config := (res-params, rel*, fun*)
