plugins {
    `java-library`
    `maven-publish`
    id("org.metaborg.convention.java")
    id("org.metaborg.convention.maven-publish")
}

fun compositeBuild(name: String) = "$group:$name:$version"
val spoofax2Version: String by ext
dependencies {
    api(platform("org.metaborg:parent:$spoofax2Version"))
    testImplementation(platform("org.metaborg:parent:$spoofax2Version"))
    annotationProcessor(platform("org.metaborg:parent:$spoofax2Version"))
    testAnnotationProcessor(platform("org.metaborg:parent:$spoofax2Version"))

    // !! Update dependencies in pom.xml as well

    implementation(compositeBuild("org.metaborg.util"))
    api(compositeBuild("org.spoofax.terms"))
    api(compositeBuild("org.spoofax.interpreter.core"))
    implementation(compositeBuild("org.strategoxt.strj"))
    api(project(":nabl2.terms"))
    api(project(":scopegraph"))
    api("io.usethesource:capsule")

    // Annotation processing
    annotationProcessor("org.immutables:value")
    annotationProcessor("org.immutables:serial")
    compileOnly("org.immutables:value")
    compileOnly("org.immutables:serial")
    implementation("jakarta.annotation:jakarta.annotation-api")

    // Tests
    testImplementation("junit:junit")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:5.1.0")
    testImplementation("ch.qos.logback:logback-classic")

    // Test Annotation processing
    testAnnotationProcessor("org.immutables:value")
    testAnnotationProcessor("org.immutables:serial")
    testCompileOnly("org.immutables:value")
    testCompileOnly("org.immutables:serial")

    // !! Update dependencies in pom.xml as well
}

// Copy test resources into classes directory, to make them accessible as classloader resources at runtime.
val copyTestResourcesTask = tasks.create<Copy>("copyTestResources") {
    from("$projectDir/src/test/resources")
    into("$buildDir/classes/java/test")
}
tasks.getByName("processTestResources").dependsOn(copyTestResourcesTask)

// FIX: `<any>` being generated by immutables
// https://github.com/immutables/immutables/issues/1490#issuecomment-1825236272
tasks.withType<JavaCompile> {
    options.sourcepath = files(sourceSets["main"].java.srcDirs)
}
