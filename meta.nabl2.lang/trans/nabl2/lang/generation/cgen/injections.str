module nabl2/lang/generation/cgen/injections

imports

  nabl2/shared/main
  
  signatures/nabl2/lang/common/-
  signatures/nabl2/lang/rules/-
  signatures/nabl2/shared/constraints/-
  generation/nabl2/lang/cgen/-
  generation/nabl2/lang/-
  nabl2/lang/generation/-
  nabl2/lang/util/-
 
rules

  injection-to-term(e) = term-to-term(e)
  injection-to-term(e):
    TermIndexFrom(t) -> Term |[ <nabl2--get-ast-index> ~t ]|
  injection-to-term(e):
    NAMEWithOrigin() -> Term |[ <try(nabl2--copy-origin(|~<term-var>))> NAME() ]|
  injection-to-term(e):
    OccurrenceIndexFrom(t) -> Term |[ <nabl2--occurrence-index-of(|~<index-var>)> ~t ]|
  injection-to-term(e):
    Text(str) -> Term |[ Text(~(<unescape-message;e> str)) ]|


  injection-to-pattern(e) = pattern-to-pattern(e)

  unescape-message =
    string-replace(|"\\[","[");
    string-replace(|"\\]","]");
    string-replace(|"\\\\","\\")
